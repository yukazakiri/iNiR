pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell
import Quickshell.Services.SystemTray
import Quickshell.Widgets
import qs
import qs.services
import qs.modules.common
import qs.modules.common.functions
import qs.modules.common.widgets
import qs.modules.iris.style
import qs.modules.iris.frame
import qs.modules.iris.components
import qs.modules.iris.pieces
import qs.modules.iris.field as Field
import qs.modules.iris.bar.island as IslandParts

ClippingRectangle {
    id: root

    property string section: ""
    property string group: ""
    property bool playing: true
    readonly property real d: IrisStyle.density
    readonly property int rev: Config.revision
    readonly property string scene: root.sceneFor(root.section, root.group)
    readonly property bool available: (Config.options?.iris?.appearance?.previews ?? true) && root.scene.length > 0
    readonly property real wantedHeight: Math.round(Math.min(420 * root.d, Math.max(260 * root.d, Number(sceneLoader.item?.naturalHeight ?? 0))))
    readonly property string wallpaper: WallpaperListener.wallpaperUrlForScreen(GlobalStates.focusedScreen)

    function sceneFor(section: string, group: string): string {
        if (group.length === 0)
            return ({ dock: "dock", player: "player", desktop: "widgets", sidebars: "panels", surfaces: "controlCenter", bubbles: "bubbles" })[section] ?? ""
        const key = section + "/" + group
        return ({
            "bar/Size": "islandReserve", "bar/Interaction": "islandInteraction",
            "bar/Shape": "islandEdge", "bar/Layout": "islandEdge", "bar/Bar": "barZones",
            "appearance/Light": "light", "appearance/Shape": "fusion", "appearance/Glass": "glass",
            "bar/Desktop page": "islandPage", "bar/Pages": "islandPage", "bar/Player page": "islandPage",
            "bubbles/Behaviour": "bubbles", "bubbles/Floating": "bubbles", "bubbles/On the contour": "bubbles", "bubbles/Size": "bubbles",
            "dock/Visibility": "dock", "dock/Icons": "dock", "dock/Look": "dock",
            "desktop/Widgets": "widgets", "desktop/Overview backdrop": "backdrop", "desktop/Wallpaper gallery": "gallery",
            "surfaces/Spotlight": "spotlight", "surfaces/Control Center": "controlCenter",
            "surfaces/Cards": "cards", "surfaces/Card contents": "cards", "surfaces/Menus": "menus",
            "surfaces/Settings": "settings", "surfaces/Side panels": "panels", "surfaces/Joining": "joining",
            "surfaces/Notifications": "feedback", "surfaces/Feedback": "feedback", "surfaces/Tray": "tray",
            "player/Player": "player", "player/Bubble": "player"
        })[key] ?? ""
    }
    function opt(path: string, fallback: var): var {
        root.rev
        return Config.getNestedValue(path, fallback)
    }

    radius: IrisStyle.radiusTile
    color: IrisStyle.surfaceHigh

    Image {
        id: wallpaperImage
        anchors.fill: parent
        source: root.available ? root.wallpaper : ""
        fillMode: Image.PreserveAspectCrop
        asynchronous: true
        cache: true
        sourceSize.width: Math.round(Math.max(1, root.width) * 1.5)
        visible: root.scene !== "backdrop"
    }

    Item {
        id: world
        readonly property real fit: Math.min(1, root.width / Math.max(1, sceneLoader.item?.naturalWidth ?? 1),
            sceneLoader.item?.cropBottom ? 1 : root.height / Math.max(1, sceneLoader.item?.naturalHeight ?? 1))
        readonly property real k: world.fit >= 0.85 ? 1 : world.fit
        width: root.width / world.k
        height: root.height / world.k
        scale: world.k
        transformOrigin: Item.TopLeft
        layer.enabled: world.k < 0.999
        layer.smooth: true
        layer.mipmap: true
        layer.textureSize: Qt.size(Math.max(1, Math.ceil(root.width * 2)), Math.max(1, Math.ceil(root.height * 2)))

        Loader {
            id: sceneLoader
            anchors.fill: parent
            active: root.available
            sourceComponent: ({
                dock: dockScene, widgets: widgetsScene, backdrop: backdropScene, gallery: galleryScene,
                spotlight: spotlightScene, controlCenter: controlScene, cards: cardsScene, menus: menusScene,
                settings: settingsScene, panels: panelsScene, joining: joiningScene, feedback: feedbackScene,
                tray: trayScene, player: playerScene, islandReserve: reserveScene, islandEdge: edgeScene, barZones: zonesScene,
                light: lightScene, fusion: fusionScene, glass: glassScene, islandInteraction: interactionScene, islandPage: islandPageScene, bubbles: bubblesScene
            })[root.scene] ?? null
        }
    }

    Rectangle {
        anchors.fill: parent
        radius: root.radius
        color: "transparent"
        border.width: 1
        border.color: IrisStyle.border
    }

    component Plate: Rectangle {
        id: plate
        property string surface: ""
        property int fallbackRadius: IrisStyle.radiusSheet
        property color own: IrisStyle.accent
        readonly property color light: IrisStyle.surfaceLight(plate.surface, plate.own)
        radius: IrisStyle.surfaceRadius(plate.surface, plate.fallbackRadius)
        color: IrisStyle.bodySurface
        border.width: IrisStyle.rim.a > 0 ? 1 : 0
        border.color: IrisStyle.rim
        IrisLightWash {
            anchors.fill: parent
            radius: plate.radius
            light: plate.light
        }
    }

    component Caption: Rectangle {
        id: caption
        property string text: ""
        property string glyph: "info"
        anchors.left: parent.left
        anchors.bottom: parent.bottom
        anchors.margins: Math.round(14 * root.d)
        implicitWidth: captionRow.implicitWidth + Math.round(22 * root.d)
        implicitHeight: Math.round(28 * root.d)
        radius: height / 2
        color: IrisStyle.veilHeavy
        visible: caption.text.length > 0
        RowLayout {
            id: captionRow
            anchors.centerIn: parent
            spacing: Math.round(6 * root.d)
            MaterialSymbol { text: caption.glyph; iconSize: Math.round(14 * root.d); color: IrisStyle.subtext }
            IrisText { text: caption.text; font.pixelSize: 11.5 * IrisStyle.typeScale; color: IrisStyle.text }
        }
    }

    component OffState: Rectangle {
        id: off
        property string text: ""
        anchors.centerIn: parent
        implicitWidth: offRow.implicitWidth + Math.round(28 * root.d)
        implicitHeight: Math.round(36 * root.d)
        radius: height / 2
        color: IrisStyle.veilHeavy
        border.width: 1
        border.color: IrisStyle.border
        RowLayout {
            id: offRow
            anchors.centerIn: parent
            spacing: Math.round(8 * root.d)
            MaterialSymbol { text: "visibility_off"; iconSize: Math.round(16 * root.d); color: IrisStyle.subtext }
            IrisText { text: off.text; font.weight: Font.DemiBold }
        }
    }

    component IslandPill: Rectangle {
        id: pill
        property bool vertical: false
        width: pill.vertical ? Math.round(IrisFrame.islandBand) : Math.round(150 * root.d)
        height: pill.vertical ? Math.round(150 * root.d) : Math.round(IrisFrame.islandBand)
        radius: Math.min(width, height) / 2
        color: IrisStyle.bodySurface
        border.width: IrisStyle.rim.a > 0 ? 1 : 0
        border.color: IrisStyle.rim
        IrisClock {
            visible: !pill.vertical
            anchors.centerIn: parent
            pixelSize: 15 * IrisStyle.typeScale
            separatorColor: IrisStyle.secondaryAccent
        }
        IslandParts.IslandStackedClock {
            visible: pill.vertical
            anchors.centerIn: parent
            pixelSize: 15 * IrisStyle.typeScale
            accent: IrisStyle.secondaryAccent
        }
    }

    component Row2: RowLayout {
        id: row2
        property string glyph: "apps"
        property string title: ""
        property string detail: ""
        property bool lit: false
        Layout.fillWidth: true
        spacing: Math.round(10 * root.d)
        Rectangle {
            implicitWidth: Math.round(30 * root.d)
            implicitHeight: implicitWidth
            radius: IrisStyle.iconRadius(width)
            color: row2.lit ? IrisStyle.accent : IrisStyle.fill
            MaterialSymbol { anchors.centerIn: parent; text: row2.glyph; iconSize: Math.round(16 * root.d); color: row2.lit ? IrisStyle.onAccent : IrisStyle.text }
        }
        IrisText { Layout.fillWidth: true; text: row2.title; font.weight: row2.lit ? Font.DemiBold : Font.Normal; elide: Text.ElideRight }
        IrisText { text: row2.detail; color: IrisStyle.muted; font.pixelSize: 11.5 * IrisStyle.typeScale }
    }

    component Level: Rectangle {
        property real value: 0.6
        property color tint: IrisStyle.fillStrong
        Layout.fillWidth: true
        implicitHeight: Math.round(6 * root.d)
        radius: height / 2
        color: IrisStyle.fill
        Rectangle { width: parent.width * parent.value; height: parent.height; radius: height / 2; color: parent.tint }
    }

    component Pointer: Item {
        id: pointer
        property bool grabbing: false
        property int travel: 560
        width: Math.round(22 * root.d)
        height: width
        z: 50
        function click(): void { pointerRing.width = 0; pointerRing.opacity = 1; pointerPulse.restart() }
        Behavior on x { NumberAnimation { duration: IrisStyle.duration(pointer.travel); easing.type: Easing.InOutCubic } }
        Behavior on y { NumberAnimation { duration: IrisStyle.duration(pointer.travel); easing.type: Easing.InOutCubic } }
        Rectangle {
            id: pointerRing
            anchors.centerIn: parent
            width: 0
            height: width
            radius: width / 2
            color: "transparent"
            border.width: 2
            border.color: IrisStyle.accent
            opacity: 0
        }
        ParallelAnimation {
            id: pointerPulse
            NumberAnimation { target: pointerRing; property: "width"; to: pointer.width * 1.7; duration: IrisStyle.duration(320); easing.type: Easing.OutCubic }
            NumberAnimation { target: pointerRing; property: "opacity"; to: 0; duration: IrisStyle.duration(420); easing.type: Easing.InQuad }
        }
        MaterialSymbol {
            anchors.centerIn: parent
            text: pointer.grabbing ? "back_hand" : "arrow_selector_tool"
            fill: 1
            iconSize: pointer.width
            color: "white"
        }
    }

    component BlockRow: RowLayout {
        id: blockRow
        property string glyph: "circle"
        property string title: ""
        property string detail: ""
        Layout.fillWidth: true
        spacing: Math.round(12 * root.d)
        Item {
            implicitWidth: Math.round(40 * root.d)
            implicitHeight: implicitWidth
            Rectangle { anchors.fill: parent; radius: width / 2; color: IrisStyle.fill }
            MaterialSymbol { anchors.centerIn: parent; text: blockRow.glyph; fill: 1; iconSize: Math.round(20 * root.d); color: IrisStyle.text }
        }
        ColumnLayout {
            Layout.fillWidth: true
            spacing: Math.round(1 * root.d)
            IrisText { Layout.fillWidth: true; text: blockRow.title; font.weight: Font.DemiBold; elide: Text.ElideRight }
            IrisText { Layout.fillWidth: true; text: blockRow.detail; color: IrisStyle.muted; font.pixelSize: 11.5 * IrisStyle.typeScale; elide: Text.ElideRight }
        }
    }

    Component {
        id: dockScene
        Item {
            id: dockRoot
            readonly property string edge: IrisFrame.dockEdge
            readonly property bool vertical: dockRoot.edge === "left" || dockRoot.edge === "right"
            readonly property real naturalWidth: Math.round((dockRoot.vertical ? 620 : 760) * root.d)
            readonly property real naturalHeight: dockRoot.vertical ? Math.max(Math.round(300 * root.d), Math.round(dockRoot.length + 64 * root.d)) : Math.round(300 * root.d)
            readonly property bool enabledDock: root.opt("iris.dock.enable", true)
            readonly property bool autoHide: root.opt("iris.dock.autoHide", true)
            readonly property bool reserve: !dockRoot.autoHide && root.opt("iris.dock.reserveSpace", true)
            readonly property bool revealOnEmpty: root.opt("iris.dock.revealOnEmpty", true)
            readonly property bool notch: root.opt("iris.dock.notch", true)
            readonly property string material: String(root.opt("iris.dock.material", "inherit"))
            readonly property bool blur: dockRoot.material === "glass" || dockRoot.material === "blur"
                || (dockRoot.material === "inherit" && (root.opt("iris.dock.blur", false) || IrisStyle.glassy))
            readonly property bool badges: root.opt("iris.dock.badges", true)
            readonly property bool launcher: root.opt("iris.dock.launcher", true)
            readonly property bool magnify: root.opt("iris.dock.magnification", false)
            readonly property real icon: Math.max(28, Math.min(64, Number(root.opt("iris.dock.iconSize", 40)))) * root.d
            readonly property var apps: (TaskbarApps.apps ?? []).filter(app => app && !app.separator && String(app.appId ?? "").length > 0 && app.appId !== "SEPARATOR").slice(0, 6)
            readonly property int count: dockRoot.apps.length + (dockRoot.launcher ? 1 : 0)
            readonly property real length: dockRoot.count * (dockRoot.icon + 10 * root.d) + 16 * root.d
            readonly property real thick: dockRoot.icon + 18 * root.d
            readonly property real span: dockRoot.vertical ? height : width
            readonly property real depth: dockRoot.vertical ? width : height
            readonly property real restInset: IrisFrame.band + (dockRoot.notch ? 0 : Math.round(10 * root.d))
            property bool hidden: false
            property int hover: -1
            property real slide: dockRoot.hidden ? -dockRoot.thick - 4 : dockRoot.restInset
            Behavior on slide { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
            readonly property real alongStart: Math.round((dockRoot.span - dockRoot.length) / 2)
            readonly property real acrossAt: dockRoot.edge === "bottom" ? height - dockRoot.slide - dockRoot.thick
                : dockRoot.edge === "right" ? width - dockRoot.slide - dockRoot.thick : dockRoot.slide
            readonly property real plateX: dockRoot.vertical ? dockRoot.acrossAt : dockRoot.alongStart
            readonly property real plateY: dockRoot.vertical ? dockRoot.alongStart : dockRoot.acrossAt
            readonly property real plateW: dockRoot.vertical ? dockRoot.thick : dockRoot.length
            readonly property real plateH: dockRoot.vertical ? dockRoot.length : dockRoot.thick
            readonly property real reach: dockRoot.reserve ? dockRoot.restInset + dockRoot.thick + Math.round(10 * root.d) : IrisFrame.band

            property int step: 0
            readonly property int steps: dockRoot.count + 3
            readonly property bool gliding: dockRoot.step >= 2 && dockRoot.step < dockRoot.steps - 1
            function iconAlong(index: int): real {
                return dockRoot.alongStart + 8 * root.d + index * (dockRoot.icon + 10 * root.d) + dockRoot.icon / 2
            }
            function spot(along: real, inset: real): point {
                const across = dockRoot.edge === "bottom" ? height - inset : dockRoot.edge === "right" ? width - inset : inset
                return dockRoot.vertical ? Qt.point(across, along) : Qt.point(along, across)
            }
            function inset(side: string): real {
                if (side === dockRoot.edge) return dockRoot.reach
                return Math.round((side === "top" || side === "bottom" ? 28 : 40) * root.d)
            }
            Timer {
                running: root.playing && dockRoot.enabledDock
                interval: 620
                repeat: true
                triggeredOnStart: true
                onTriggered: dockRoot.step = (dockRoot.step + 1) % dockRoot.steps
                onRunningChanged: if (!running) dockRoot.step = 1
            }
            Binding { dockRoot.hidden: dockRoot.autoHide && dockRoot.enabledDock && (dockRoot.step === 0 || dockRoot.step === dockRoot.steps - 1) }
            Binding { dockRoot.hover: dockRoot.magnify && dockRoot.gliding ? dockRoot.step - 2 : -1 }
            Pointer {
                readonly property point aim: dockRoot.step === 0 ? Qt.point(dockRoot.width * 0.62, dockRoot.height * 0.36)
                    : dockRoot.step === dockRoot.steps - 1 ? Qt.point(dockRoot.width * 0.34, dockRoot.height * 0.3)
                    : dockRoot.step === 1 ? dockRoot.spot(dockRoot.span / 2, IrisFrame.band)
                    : dockRoot.spot(dockRoot.iconAlong(dockRoot.step - 2), dockRoot.restInset + dockRoot.thick * 0.65)
                visible: dockRoot.enabledDock && root.playing
                x: aim.x - width / 3
                y: aim.y - height / 3
            }

            Rectangle {
                x: dockRoot.inset("left")
                y: dockRoot.inset("top")
                width: parent.width - x - dockRoot.inset("right")
                height: parent.height - y - dockRoot.inset("bottom")
                radius: IrisStyle.radiusTile
                color: IrisStyle.surfaceHigh
                border.width: 1
                border.color: IrisStyle.border
                Behavior on x { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Behavior on y { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Behavior on width { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Behavior on height { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Row {
                    x: Math.round(12 * root.d); y: Math.round(11 * root.d)
                    spacing: Math.round(6 * root.d)
                    Repeater { model: 3; Rectangle { required property int index; width: Math.round(10 * root.d); height: width; radius: width / 2; color: IrisStyle.fillStrong } }
                }
                Rectangle { x: Math.round(12 * root.d); y: Math.round(38 * root.d); width: parent.width * 0.4; height: Math.round(9 * root.d); radius: height / 2; color: IrisStyle.fill }
                Rectangle { x: Math.round(12 * root.d); y: Math.round(56 * root.d); width: parent.width * 0.62; height: Math.round(9 * root.d); radius: height / 2; color: IrisStyle.fillQuiet }
                Rectangle {
                    visible: dockRoot.reserve
                    x: dockRoot.edge === "right" ? parent.width - 1 : 0
                    y: dockRoot.edge === "bottom" ? parent.height - 1 : 0
                    width: dockRoot.vertical ? 1 : parent.width
                    height: dockRoot.vertical ? parent.height : 1
                    color: IrisStyle.accent
                }
            }

            ClippingRectangle {
                visible: dockRoot.blur && dockRoot.enabledDock
                x: dockRoot.plateX; y: dockRoot.plateY
                width: dockRoot.plateW; height: dockRoot.plateH
                radius: dockRoot.notch ? Math.round(16 * root.d) : dockRoot.thick / 2
                color: "transparent"
                Image {
                    id: dockBlurSource
                    x: -dockRoot.plateX; y: -dockRoot.plateY
                    width: dockRoot.width; height: dockRoot.height
                    source: root.wallpaper
                    fillMode: Image.PreserveAspectCrop
                    sourceSize.width: 400
                    visible: false
                }
                MultiEffect { anchors.fill: dockBlurSource; source: dockBlurSource; blurEnabled: true; blur: 1; blurMax: 48 }
            }

            Field.IrisField {
                anchors.fill: parent
                visible: dockRoot.enabledDock
                framed: false
                tint: dockRoot.blur ? IrisStyle.veilStrong : IrisStyle.bodySurface
                shapes: {
                    const out = []
                    const deep = Math.max(8, IrisStyle.fuseDeep * 2)
                    const f = IrisStyle.fuseDeep
                    const W = dockRoot.width, H = dockRoot.height, band = IrisFrame.band
                    out.push(dockRoot.edge === "top" ? { x: -2 * f, y: -deep, width: W + 4 * f, height: band + deep }
                        : dockRoot.edge === "left" ? { x: -deep, y: -2 * f, width: band + deep, height: H + 4 * f }
                        : dockRoot.edge === "right" ? { x: W - band, y: -2 * f, width: band + deep, height: H + 4 * f }
                        : { x: -2 * f, y: H - band, width: W + 4 * f, height: band + deep })
                    Object.assign(out[0], { radius: 0, fuse: f, id: "edge", paints: true })
                    out.push({ x: dockRoot.plateX, y: dockRoot.plateY, width: dockRoot.plateW, height: dockRoot.plateH,
                        radius: dockRoot.notch ? Math.round(16 * root.d) : dockRoot.thick / 2,
                        fuse: dockRoot.notch ? IrisStyle.fuseEdge : IrisStyle.fuse, id: "dock", joins: dockRoot.notch ? "edge" : "", paints: true })
                    return out
                }
            }

            Grid {
                visible: dockRoot.enabledDock
                columns: dockRoot.vertical ? 1 : Math.max(1, dockRoot.count)
                x: dockRoot.vertical ? dockRoot.plateX + (dockRoot.thick - dockRoot.icon) / 2 : dockRoot.plateX + 8 * root.d
                y: dockRoot.vertical ? dockRoot.plateY + 8 * root.d : dockRoot.plateY + (dockRoot.thick - dockRoot.icon) / 2
                spacing: Math.round(10 * root.d)
                Repeater {
                    model: (dockRoot.launcher ? [{ launcher: true }] : []).concat(dockRoot.apps)
                    Item {
                        id: dockIcon
                        required property var modelData
                        required property int index
                        readonly property int distance: dockRoot.hover < 0 ? 9 : Math.abs(dockRoot.hover - dockIcon.index)
                        width: dockRoot.icon
                        height: width
                        scale: dockIcon.distance === 0 ? 1.45 : dockIcon.distance === 1 ? 1.18 : 1
                        transformOrigin: ({ top: Item.Top, left: Item.Left, right: Item.Right })[dockRoot.edge] ?? Item.Bottom
                        z: 3 - Math.min(3, dockIcon.distance)
                        Behavior on scale { NumberAnimation { duration: IrisStyle.duration(160); easing.type: IrisStyle.feedbackEasing } }
                        Rectangle {
                            anchors.fill: parent
                            visible: dockIcon.modelData.launcher === true
                            radius: IrisStyle.iconRadius(width)
                            color: IrisStyle.fill
                            MaterialSymbol { anchors.centerIn: parent; text: "apps"; iconSize: Math.round(dockRoot.icon * 0.5); color: IrisStyle.text }
                        }
                        SmartAppIcon {
                            anchors.fill: parent
                            visible: dockIcon.modelData.launcher !== true
                            implicitSize: parent.width
                            icon: IrisPieces.appIcon(dockIcon.modelData.appId ?? "")
                            fallback: "application-x-executable"
                        }
                        Rectangle {
                            visible: dockRoot.badges && dockIcon.modelData.launcher !== true && dockIcon.index % 3 === 1
                            anchors.right: parent.right
                            anchors.top: parent.top
                            anchors.margins: -Math.round(3 * root.d)
                            width: Math.round(17 * root.d); height: width; radius: width / 2
                            color: IrisStyle.badge
                            IrisText { anchors.centerIn: parent; text: dockIcon.index; color: IrisStyle.onBadge; font.pixelSize: 10 * IrisStyle.typeScale; font.weight: Font.Bold }
                        }
                    }
                }
            }

            OffState { visible: !dockRoot.enabledDock; text: Translation.tr("Dock off") }
            Caption {
                anchors.leftMargin: dockRoot.edge === "left" ? dockRoot.restInset + dockRoot.thick + Math.round(14 * root.d) : Math.round(14 * root.d)
                glyph: dockRoot.autoHide ? "unfold_less" : !dockRoot.reserve ? "layers"
                    : ({ top: "vertical_align_top", left: "align_horizontal_left", right: "align_horizontal_right" })[dockRoot.edge] ?? "vertical_align_bottom"
                text: !dockRoot.enabledDock ? ""
                    : dockRoot.autoHide ? (dockRoot.revealOnEmpty ? Translation.tr("Hides over windows · stays on empty workspaces") : Translation.tr("Hides until the pointer reaches the edge"))
                    : !dockRoot.reserve ? Translation.tr("Windows run under the Dock")
                    : dockRoot.vertical ? Translation.tr("Windows stop beside the Dock")
                    : dockRoot.edge === "top" ? Translation.tr("Windows start below the Dock") : Translation.tr("Windows stop above the Dock")
            }
        }
    }

    component Loop: SequentialAnimation {
        id: loop
        property real rest: 1400
        loops: Animation.Infinite
        NumberAnimation { to: 1; duration: IrisStyle.duration(IrisStyle.settleDuration * 2); easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.emergeCurve }
        PauseAnimation { duration: loop.rest }
        NumberAnimation { to: 0; duration: IrisStyle.duration(IrisStyle.recedeDuration * 2); easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.recedeCurve }
        PauseAnimation { duration: loop.rest / 3 }
    }

    component LitPlate: Rectangle {
        id: lit
        property color light: IrisStyle.wallpaperLight
        color: IrisStyle.bodySurface
        border.width: IrisStyle.rim.a > 0 ? 1 : 0
        border.color: IrisStyle.rim
        IrisLightWash {
            anchors.fill: parent
            radius: lit.radius
            light: lit.light
        }
    }

    component LitBody: Item {
        id: body
        property color light
        property real restWidth: 0
        property real restHeight: 0
        property real progress: 1
        default property alias content: bodyContent.data
        RectangularShadow {
            anchors.fill: bodyPlate
            radius: bodyPlate.radius
            offset.y: 3 * IrisStyle.density
            blur: 16 * IrisStyle.density
            color: IrisStyle.shadow
        }
        LitPlate {
            id: bodyPlate
            anchors.fill: parent
            radius: Math.min(IrisStyle.radiusSheet, height / 2)
            light: body.light
        }
        Item {
            id: bodyContent
            readonly property real pad: IrisStyle.concentricPad(IrisStyle.radiusSheet, 14 * root.d)
            x: bodyContent.pad
            y: bodyContent.pad
            width: body.restWidth - 2 * bodyContent.pad
            height: body.restHeight - 2 * bodyContent.pad
            opacity: IrisStyle.ramp(body.progress, 0.55, 0.45)
            visible: opacity > 0
        }
    }

    Component {
        id: edgeScene
        Item {
            id: edgeRoot
            readonly property real naturalWidth: Math.round(640 * root.d)
            readonly property real naturalHeight: Math.round(320 * root.d)
            readonly property string edge: IrisFrame.islandEdge
            readonly property bool vertical: edgeRoot.edge === "left" || edgeRoot.edge === "right"
            readonly property bool notch: root.opt("iris.bar.notch", false)
            readonly property string layout: String(root.opt("iris.bar.layout", "island"))
            readonly property real thick: IrisFrame.islandBand
            readonly property real span: edgeRoot.vertical ? height : width
            readonly property real length: edgeRoot.layout === "full" ? edgeRoot.span - 2 * IrisFrame.band
                : Math.round(edgeRoot.thick * (edgeRoot.vertical ? 3.2 : 3.6))
            readonly property real along: edgeRoot.layout === "full" ? IrisFrame.band
                : edgeRoot.layout === "left" ? IrisFrame.band + Math.round(20 * root.d)
                : edgeRoot.layout === "right" ? edgeRoot.span - edgeRoot.length - IrisFrame.band - Math.round(20 * root.d)
                : Math.round((edgeRoot.span - edgeRoot.length) / 2)
            readonly property real inset: IrisFrame.band + IrisFrame.islandMargin
            property real t: 0
            Loop on t { running: root.playing }
            readonly property real depth: -edgeRoot.thick - 4 + (edgeRoot.inset + edgeRoot.thick + 4) * edgeRoot.t
            function place(along: real, length: real, depth: real, thick: real): var {
                const across = edgeRoot.edge === "bottom" ? height - depth - thick : edgeRoot.edge === "right" ? width - depth - thick : depth
                return edgeRoot.vertical ? { x: across, y: along, width: thick, height: length } : { x: along, y: across, width: length, height: thick }
            }
            readonly property var island: edgeRoot.place(edgeRoot.along, edgeRoot.length, edgeRoot.depth, edgeRoot.thick)
            readonly property real bubble: Math.round(edgeRoot.thick * 0.86)
            readonly property var satellites: edgeRoot.layout === "full" ? [] : [
                edgeRoot.place(edgeRoot.along - Math.round(6 * root.d) - edgeRoot.bubble, edgeRoot.bubble, edgeRoot.depth + (edgeRoot.thick - edgeRoot.bubble) / 2, edgeRoot.bubble),
                edgeRoot.place(edgeRoot.along + edgeRoot.length + Math.round(6 * root.d), edgeRoot.bubble, edgeRoot.depth + (edgeRoot.thick - edgeRoot.bubble) / 2, edgeRoot.bubble)
            ]
            function edgeBody(): var {
                const deep = Math.max(8, IrisStyle.fuseDeep * 2), f = IrisStyle.fuseDeep
                switch (edgeRoot.edge) {
                case "bottom": return { x: -2 * f, y: height, width: width + 4 * f, height: deep }
                case "left": return { x: -deep, y: -2 * f, width: deep, height: height + 4 * f }
                case "right": return { x: width, y: -2 * f, width: deep, height: height + 4 * f }
                default: return { x: -2 * f, y: -deep, width: width + 4 * f, height: deep }
                }
            }
            Field.IrisField {
                anchors.fill: parent
                framed: false
                shapes: {
                    const out = []
                    if (edgeRoot.notch) out.push(Object.assign({ radius: 0, fuse: IrisStyle.fuseDeep, id: "edge", paints: true }, edgeRoot.edgeBody()))
                    out.push(Object.assign({ radius: edgeRoot.layout === "full" ? 0 : edgeRoot.thick / 2, fuse: edgeRoot.notch ? IrisStyle.fuseEdge : IrisStyle.fuse,
                        id: "island", joins: edgeRoot.notch ? "edge" : "", paints: true }, edgeRoot.island))
                    edgeRoot.satellites.forEach((sat, i) => out.push(Object.assign({ radius: IrisStyle.pieceRadius(edgeRoot.bubble), fuse: IrisStyle.fuse,
                        id: "satellite" + i, joins: "island", paints: true }, sat)))
                    return out
                }
            }
            IrisClock {
                visible: !edgeRoot.vertical
                opacity: edgeRoot.t
                x: edgeRoot.island.x + (edgeRoot.island.width - width) / 2
                y: edgeRoot.island.y + (edgeRoot.island.height - height) / 2
                pixelSize: 15 * IrisStyle.typeScale
                separatorColor: IrisStyle.secondaryAccent
            }
            IslandParts.IslandStackedClock {
                visible: edgeRoot.vertical
                opacity: edgeRoot.t
                x: edgeRoot.island.x + (edgeRoot.island.width - width) / 2
                y: edgeRoot.island.y + (edgeRoot.island.height - height) / 2
                pixelSize: 15 * IrisStyle.typeScale
                accent: IrisStyle.secondaryAccent
            }
            Caption {
                anchors.leftMargin: edgeRoot.edge === "left" ? edgeRoot.inset + edgeRoot.thick + Math.round(14 * root.d) : Math.round(14 * root.d)
                glyph: ({ top: "vertical_align_top", bottom: "vertical_align_bottom", left: "align_horizontal_left", right: "align_horizontal_right" })[edgeRoot.edge] ?? "pill"
                text: Translation.tr(({ top: "Top edge", bottom: "Bottom edge", left: "Left edge", right: "Right edge" })[edgeRoot.edge] ?? "")
                    + " · " + (edgeRoot.layout === "full" ? Translation.tr("a bar across it") : edgeRoot.notch ? Translation.tr("melts into it") : Translation.tr("floats off it"))
            }
        }
    }

    Component {
        id: zonesScene
        Item {
            id: zonesRoot
            readonly property real naturalWidth: Math.round(680 * root.d)
            readonly property real naturalHeight: Math.round(220 * root.d)
            readonly property var names: ({ island: "Island", workspaces: "Workspaces", window: "Window", time: "Time", tray: "Tray",
                notifications: "Notifications", sound: "Sound", controls: "Controls", mic: "Mic", weather: "Weather", tools: "Tools", media: "Media" })
            function listOf(path: string, fallback: var): var { return Array.from(root.opt(path, fallback) ?? []).map(kind => String(kind)) }
            readonly property var zones: [
                zonesRoot.listOf("iris.bar.fullStart", ["workspaces", "window"]),
                zonesRoot.listOf("iris.bar.fullCenter", ["island"]),
                zonesRoot.listOf("iris.bar.fullEnd", ["tray", "notifications", "sound", "controls"])
            ]
            readonly property real thick: IrisFrame.islandBand
            property real t: 0
            NumberAnimation on t { running: root.playing; from: 0; to: 3; duration: IrisStyle.duration(5400); loops: Animation.Infinite }
            readonly property int lit: Math.min(2, Math.floor(zonesRoot.t))
            Rectangle {
                id: bar
                x: Math.round(16 * root.d)
                y: Math.round(28 * root.d)
                width: parent.width - 2 * x
                height: zonesRoot.thick
                radius: IrisStyle.radiusChip
                color: IrisStyle.bodySurface
                border.width: IrisStyle.rim.a > 0 ? 1 : 0
                border.color: IrisStyle.rim
                Repeater {
                    model: 3
                    Row {
                        id: zone
                        required property int index
                        readonly property var kinds: zonesRoot.zones[zone.index]
                        spacing: Math.round(6 * root.d)
                        anchors.verticalCenter: parent.verticalCenter
                        x: zone.index === 0 ? Math.round(8 * root.d) : zone.index === 1 ? Math.round((bar.width - width) / 2) : bar.width - width - Math.round(8 * root.d)
                        Repeater {
                            model: zone.kinds
                            Rectangle {
                                required property string modelData
                                readonly property bool current: zonesRoot.lit === zone.index
                                implicitWidth: chipLabel.implicitWidth + Math.round(16 * root.d)
                                implicitHeight: zonesRoot.thick - Math.round(10 * root.d)
                                radius: height / 2
                                color: current ? IrisStyle.tintFill(IrisStyle.accent) : IrisStyle.fill
                                Behavior on color { ColorAnimation { duration: IrisStyle.duration(160) } }
                                IrisText {
                                    id: chipLabel
                                    anchors.centerIn: parent
                                    text: modelData === "time" ? Qt.formatTime(new Date(), "hh:mm") : Translation.tr(zonesRoot.names[modelData] ?? modelData)
                                    font.pixelSize: 11.5 * IrisStyle.typeScale
                                    color: parent.current ? IrisStyle.text : IrisStyle.subtext
                                }
                            }
                        }
                    }
                }
            }
            Caption {
                glyph: ["align_horizontal_left", "align_horizontal_center", "align_horizontal_right"][zonesRoot.lit]
                text: [Translation.tr("Start"), Translation.tr("Centre"), Translation.tr("End")][zonesRoot.lit] + ": "
                    + (zonesRoot.zones[zonesRoot.lit].length > 0 ? zonesRoot.zones[zonesRoot.lit].map(kind => Translation.tr(zonesRoot.names[kind] ?? kind)).join(", ") : Translation.tr("empty"))
            }
        }
    }

    Component {
        id: lightScene
        Item {
            id: lightRoot
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(290 * root.d)
            readonly property string aura: String(root.opt("iris.appearance.aura", "subtle"))
            readonly property real reach: Number(root.opt("iris.appearance.theme.lightReach", 100))
            readonly property real glow: Number(root.opt("iris.appearance.theme.glow", 0))
            readonly property real bodyWidth: Math.round(236 * root.d)
            readonly property real bodyHeight: Math.round(176 * root.d)
            readonly property real gutter: Math.round((lightRoot.width - 2 * lightRoot.bodyWidth) / 3)
            property real t: 0
            Loop on t { running: root.playing }
            readonly property real grown: IrisFrame.islandBand + (lightRoot.bodyHeight - IrisFrame.islandBand) * lightRoot.t
            readonly property real wide: IrisFrame.islandBand * 2 + (lightRoot.bodyWidth - IrisFrame.islandBand * 2) * lightRoot.t

            LitBody {
                x: lightRoot.gutter + (lightRoot.bodyWidth - width) / 2
                y: Math.round(26 * root.d)
                width: lightRoot.wide
                height: lightRoot.grown
                restWidth: lightRoot.bodyWidth
                restHeight: lightRoot.bodyHeight
                progress: lightRoot.t
                light: IrisStyle.identity.sky
                ColumnLayout {
                    anchors.fill: parent
                    spacing: Math.round(2 * root.d)
                    RowLayout {
                        spacing: Math.round(6 * root.d)
                        MaterialSymbol { text: "partly_cloudy_day"; fill: 1; iconSize: Math.round(18 * root.d); color: IrisStyle.identity.sky }
                        IrisText { text: Translation.tr("Weather"); font.weight: Font.DemiBold }
                    }
                    IrisText {
                        text: "18°"
                        font.family: IrisStyle.fontNumbers
                        font.weight: IrisStyle.figureWeight
                        font.pixelSize: 40 * IrisStyle.typeScale
                    }
                    IrisText { text: Translation.tr("Partly cloudy"); color: IrisStyle.muted; font.pixelSize: 12 * IrisStyle.typeScale }
                    Item { Layout.fillHeight: true }
                }
            }
            LitBody {
                x: 2 * lightRoot.gutter + lightRoot.bodyWidth + (lightRoot.bodyWidth - width) / 2
                y: Math.round(26 * root.d)
                width: lightRoot.wide
                height: lightRoot.grown
                restWidth: lightRoot.bodyWidth
                restHeight: lightRoot.bodyHeight
                progress: lightRoot.t
                light: IrisStyle.wallpaperLight
                ColumnLayout {
                    anchors.fill: parent
                    spacing: Math.round(10 * root.d)
                    RowLayout {
                        spacing: Math.round(6 * root.d)
                        MaterialSymbol { text: "tune"; iconSize: Math.round(18 * root.d); color: IrisStyle.text }
                        IrisText { text: Translation.tr("Control Center"); font.weight: Font.DemiBold }
                    }
                    RowLayout {
                        spacing: Math.round(8 * root.d)
                        Repeater {
                            model: ["wifi", "bluetooth", "dark_mode"]
                            Rectangle {
                                required property string modelData
                                required property int index
                                implicitWidth: Math.round(38 * root.d)
                                implicitHeight: implicitWidth
                                radius: IrisStyle.pieceRadius(implicitWidth)
                                color: index === 0 ? IrisStyle.accent : IrisStyle.fill
                                MaterialSymbol { anchors.centerIn: parent; text: parent.modelData; fill: 1; iconSize: Math.round(18 * root.d); color: parent.index === 0 ? IrisStyle.onAccent : IrisStyle.text }
                            }
                        }
                    }
                    Rectangle {
                        Layout.fillWidth: true
                        implicitHeight: Math.round(26 * root.d)
                        radius: height / 2
                        color: IrisStyle.fill
                        Rectangle { width: parent.width * 0.62; height: parent.height; radius: height / 2; color: IrisStyle.fillActive }
                    }
                    Item { Layout.fillHeight: true }
                }
            }
            Caption {
                glyph: lightRoot.aura === "off" ? "light_off" : "light_mode"
                text: (lightRoot.aura === "off" ? Translation.tr("Light off")
                    : (lightRoot.aura === "vivid" ? Translation.tr("Vivid") : Translation.tr("Subtle")) + " · " + Translation.tr("reach %1%").arg(Math.round(lightRoot.reach)))
                    + " · " + (lightRoot.glow > 0 ? Translation.tr("shadows glow %1%").arg(Math.round(lightRoot.glow)) : Translation.tr("dark shadows"))
            }
        }
    }

    Component {
        id: fusionScene
        Item {
            id: fuseRoot
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            readonly property real melt: Number(root.opt("iris.appearance.theme.melt", 0))
            readonly property real corners: Number(root.opt("iris.appearance.theme.shape", 100))
            readonly property real bubble: IrisFrame.islandBand
            property real t: 0
            Loop on t { running: root.playing; rest: 900 }
            readonly property var island: ({ x: Math.round(width / 2 - 90 * root.d), y: Math.round(24 * root.d), width: Math.round(180 * root.d), height: fuseRoot.bubble })
            readonly property var satellite: ({ x: fuseRoot.island.x + fuseRoot.island.width + Math.round(6 * root.d), y: fuseRoot.island.y, width: fuseRoot.bubble, height: fuseRoot.bubble })
            readonly property real cardTop: fuseRoot.island.y + fuseRoot.island.height + Math.round(40 * root.d) * (1 - fuseRoot.t) - IrisStyle.weld * fuseRoot.t
            readonly property var card: ({ x: Math.round(width / 2 - 130 * root.d), y: fuseRoot.cardTop, width: Math.round(260 * root.d), height: Math.round(150 * root.d) })
            Field.IrisField {
                anchors.fill: parent
                framed: false
                shapes: [
                    Object.assign({ radius: fuseRoot.bubble / 2, fuse: IrisStyle.fuse, id: "island", paints: true }, fuseRoot.island),
                    Object.assign({ radius: IrisStyle.pieceRadius(fuseRoot.bubble), fuse: IrisStyle.fuse, id: "satellite", joins: "island", paints: true }, fuseRoot.satellite),
                    Object.assign({ radius: IrisStyle.radiusSheet, fuse: IrisStyle.fuseDeep, id: "card", joins: "island", paints: true }, fuseRoot.card)
                ]
            }
            IrisClock {
                x: fuseRoot.island.x + (fuseRoot.island.width - width) / 2
                y: fuseRoot.island.y + (fuseRoot.island.height - height) / 2
                pixelSize: 15 * IrisStyle.typeScale
                separatorColor: IrisStyle.secondaryAccent
            }
            Column {
                x: fuseRoot.card.x + IrisStyle.concentricPad(IrisStyle.radiusSheet, 14 * root.d)
                y: fuseRoot.card.y + IrisStyle.concentricPad(IrisStyle.radiusSheet, 14 * root.d)
                spacing: Math.round(8 * root.d)
                Rectangle { width: Math.round(120 * root.d); height: Math.round(10 * root.d); radius: IrisStyle.radiusMicro; color: IrisStyle.fillHover }
                Rectangle { width: Math.round(170 * root.d); height: Math.round(10 * root.d); radius: IrisStyle.radiusMicro; color: IrisStyle.fill }
                Row {
                    spacing: Math.round(8 * root.d)
                    Repeater { model: 3; Rectangle { required property int index; width: Math.round(46 * root.d); height: Math.round(34 * root.d); radius: IrisStyle.radiusTile; color: IrisStyle.fillQuiet } }
                }
            }
            Caption {
                glyph: "join_inner"
                text: (fuseRoot.melt > 0 ? Translation.tr("Fusion %1%").arg(Math.round(fuseRoot.melt)) : Translation.tr("Crisp joins"))
                    + " · " + Translation.tr("corners %1%").arg(Math.round(fuseRoot.corners))
            }
        }
    }

    Component {
        id: glassScene
        Item {
            id: glassRoot
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(280 * root.d)
            readonly property string mode: String(root.opt("iris.appearance.glass.mode", "off"))
            property real t: 0
            Loop on t { running: root.playing; rest: 600 }
            ClippingRectangle {
                id: pane
                width: Math.round(300 * root.d)
                height: Math.round(170 * root.d)
                x: Math.round(24 * root.d + (glassRoot.width - width - 48 * root.d) * glassRoot.t)
                y: Math.round(28 * root.d)
                radius: IrisStyle.radiusSheet
                color: IrisStyle.glassy ? "transparent" : IrisStyle.bodySurface
                Image {
                    id: paneSource
                    x: -pane.x; y: -pane.y
                    width: glassRoot.width; height: glassRoot.height
                    source: IrisStyle.glassy ? root.wallpaper : ""
                    fillMode: Image.PreserveAspectCrop
                    sourceSize.width: 400
                    visible: false
                }
                MultiEffect {
                    anchors.fill: paneSource
                    visible: IrisStyle.glassy
                    source: paneSource
                    blurEnabled: true
                    blur: IrisStyle.glassBlurAmount
                    blurMax: IrisStyle.glassBlurMax
                    saturation: IrisStyle.glassSaturation
                }
                Rectangle { anchors.fill: parent; visible: IrisStyle.glassy; color: IrisStyle.bodyTint }
                Column {
                    x: IrisStyle.concentricPad(pane.radius, 14 * root.d)
                    y: x
                    width: pane.width - 2 * x
                    spacing: Math.round(4 * root.d)
                    IrisText { text: Translation.tr("Now playing"); font.weight: Font.DemiBold; font.pixelSize: 14 * IrisStyle.typeScale }
                    IrisText { width: parent.width; text: Translation.tr("Secondary text stays readable over the wallpaper"); color: IrisStyle.muted; wrapMode: Text.WordWrap; font.pixelSize: 12 * IrisStyle.typeScale }
                    IrisText { text: Translation.tr("Tertiary detail"); color: IrisStyle.textTertiary; font.pixelSize: 11.5 * IrisStyle.typeScale }
                }
            }
            Caption {
                glyph: glassRoot.mode === "off" ? "crop_square" : "blur_on"
                text: glassRoot.mode === "off" ? Translation.tr("Off: solid material")
                    : Translation.tr("Tint %1% · frost %2%").arg(Math.round(IrisStyle.glassTint * 100)).arg(Math.round(IrisStyle.glassBlurAmount * 100))
                        + (glassRoot.mode === "compositor" ? " · " + Translation.tr("Blur previews as Glass") : "")
            }
        }
    }

    Component {
        id: widgetsScene
        Item {
            id: widgetsRoot
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(260 * root.d)
            readonly property bool on: root.opt("iris.modules.desktopWidgets", true)
            readonly property string material: String(root.opt("iris.widgets.material", "glass"))
            readonly property bool glass: widgetsRoot.material === "glass"
            readonly property bool clear: widgetsRoot.material === "clear"
            readonly property color ink: String(root.opt("iris.widgets.tint", "wallpaper")) === "wallpaper" ? IrisStyle.wallpaperLight : IrisStyle.accent
            readonly property int weight: ({ light: Font.Light, regular: Font.Medium, bold: Font.Bold })[String(root.opt("iris.widgets.weight", "regular"))] ?? Font.Medium
            readonly property real strength: Math.max(0.2, Math.min(1, Number(root.opt("iris.widgets.opacity", 100)) / 100))
            readonly property real plateRadius: Math.round(Math.max(0, Math.min(40, Number(root.opt("iris.widgets.radius", 22)))) * root.d)
            readonly property real unit: Math.round(170 * root.d)
            readonly property real wide: Math.round(250 * root.d)
            readonly property real gap: Math.round(16 * root.d)
            readonly property real plateX: Math.round((widgetsRoot.width - widgetsRoot.wide - widgetsRoot.gap - widgetsRoot.unit) / 2)
            readonly property real plateY: Math.round((widgetsRoot.height - widgetsRoot.unit) / 2)
            readonly property color plateColor: widgetsRoot.glass || widgetsRoot.clear
                ? ColorUtils.applyAlpha(IrisStyle.surface, IrisStyle.legibleVeil(widgetsRoot.material, 0, 0, widgetsRoot.strength))
                : ColorUtils.applyAlpha(widgetsRoot.material === "tinted"
                    ? ColorUtils.mix(IrisStyle.surface, Appearance.colors.colPrimary, 0.82) : IrisStyle.surface, widgetsRoot.strength)

            Image {
                id: widgetsWall
                anchors.fill: parent
                visible: false
                source: widgetsRoot.glass ? root.wallpaper : ""
                fillMode: Image.PreserveAspectCrop
                sourceSize.width: 700
                asynchronous: true
            }
            Repeater {
                model: widgetsRoot.on && widgetsRoot.glass ? [
                    Qt.rect(widgetsRoot.plateX, widgetsRoot.plateY, widgetsRoot.wide, widgetsRoot.unit),
                    Qt.rect(widgetsRoot.plateX + widgetsRoot.wide + widgetsRoot.gap, widgetsRoot.plateY, widgetsRoot.unit, widgetsRoot.unit)
                ] : []
                ClippingRectangle {
                    id: frost
                    required property rect modelData
                    x: frost.modelData.x
                    y: frost.modelData.y
                    width: frost.modelData.width
                    height: frost.modelData.height
                    radius: widgetsRoot.plateRadius
                    color: "transparent"
                    MultiEffect {
                        x: -frost.x
                        y: -frost.y
                        width: widgetsRoot.width
                        height: widgetsRoot.height
                        source: widgetsWall
                        autoPaddingEnabled: false
                        blurEnabled: true
                        blur: IrisStyle.glassBlur
                        blurMax: IrisStyle.glassBlurMax
                        saturation: IrisStyle.glassSaturation
                    }
                }
            }

            Rectangle {
                visible: widgetsRoot.on
                x: widgetsRoot.plateX
                y: widgetsRoot.plateY
                width: widgetsRoot.wide
                height: widgetsRoot.unit
                radius: widgetsRoot.plateRadius
                color: widgetsRoot.plateColor
                border.width: widgetsRoot.clear ? 0 : 1
                border.color: IrisStyle.rim
                ColumnLayout {
                    anchors.left: parent.left; anchors.bottom: parent.bottom
                    anchors.margins: Math.round(18 * root.d)
                    spacing: 0
                    IrisText { text: Qt.locale().toString(DateTime.clock.date, "dddd"); color: widgetsRoot.ink; font.weight: widgetsRoot.weight; font.pixelSize: 15 * IrisStyle.typeScale }
                    IrisText {
                        text: Qt.locale().toString(DateTime.clock.date, "hh:mm")
                        font.family: IrisStyle.fontNumbers
                        font.weight: widgetsRoot.weight
                        font.pixelSize: 52 * IrisStyle.typeScale
                        style: widgetsRoot.clear ? Text.Raised : Text.Normal
                        styleColor: IrisStyle.plateShadow
                    }
                }
            }
            Rectangle {
                visible: widgetsRoot.on
                x: widgetsRoot.plateX + widgetsRoot.wide + widgetsRoot.gap
                y: widgetsRoot.plateY
                width: widgetsRoot.unit
                height: widgetsRoot.unit
                radius: widgetsRoot.plateRadius
                color: widgetsRoot.plateColor
                border.width: widgetsRoot.clear ? 0 : 1
                border.color: IrisStyle.rim
                ColumnLayout {
                    anchors.fill: parent
                    anchors.margins: Math.round(18 * root.d)
                    spacing: Math.round(2 * root.d)
                    MaterialSymbol { text: Icons.getWeatherIcon(Weather.data?.wCode, Weather.isNightNow()) ?? "cloud"; fill: 1; iconSize: Math.round(30 * root.d); color: widgetsRoot.ink }
                    Item { Layout.fillHeight: true }
                    IrisText { text: String(Weather.data?.temp ?? "18°"); font.family: IrisStyle.fontNumbers; font.weight: widgetsRoot.weight; font.pixelSize: 34 * IrisStyle.typeScale }
                    IrisText { text: Weather.data?.city ?? Translation.tr("Weather"); color: IrisStyle.subtext; elide: Text.ElideRight; Layout.fillWidth: true }
                }
            }
            OffState { visible: !widgetsRoot.on; text: Translation.tr("Desktop widgets off") }
            Caption {
                glyph: widgetsRoot.glass ? "blur_on" : widgetsRoot.clear ? "select" : "square"
                text: !widgetsRoot.on ? ""
                    : widgetsRoot.glass ? Translation.tr("Frosted wallpaper · darker only where the wallpaper is bright")
                    : widgetsRoot.clear ? Translation.tr("Bare wallpaper · a veil only where text needs it")
                    : widgetsRoot.material === "tinted" ? Translation.tr("Black material with a trace of the wallpaper hue")
                    : Translation.tr("The Island's black material")
            }
        }
    }

    Component {
        id: backdropScene
        Item {
            id: backdropRoot
            readonly property real naturalWidth: Math.round(640 * root.d)
            readonly property real naturalHeight: Math.round(260 * root.d)
            readonly property bool on: root.opt("background.backdrop.enable", true)
            readonly property real blurAmount: Math.max(0, Math.min(100, Number(root.opt("background.backdrop.blurRadius", 40))))
            readonly property real dim: Math.max(0, Math.min(100, Number(root.opt("background.backdrop.dim", 40)))) / 100
            readonly property bool vignette: root.opt("background.backdrop.vignetteEnabled", false)
            Rectangle { anchors.fill: parent; color: IrisStyle.surface }
            Item {
                anchors.fill: parent
                visible: backdropRoot.on
                clip: true
                Image {
                    id: backdropImage
                    anchors.fill: parent
                    anchors.margins: -48
                    source: root.wallpaper
                    fillMode: Image.PreserveAspectCrop
                    sourceSize.width: 700
                    visible: false
                }
                MultiEffect {
                    anchors.fill: backdropImage
                    source: backdropImage
                    blurEnabled: backdropRoot.blurAmount > 0
                    blur: backdropRoot.blurAmount / 100
                    blurMax: 48
                }
                Rectangle { anchors.fill: parent; color: "black"; opacity: backdropRoot.dim }
                Rectangle {
                    anchors.fill: parent
                    visible: backdropRoot.vignette
                    gradient: Gradient {
                        GradientStop { position: 0; color: Qt.rgba(0, 0, 0, 0.55) } // iris-literal: vignette falloff
                        GradientStop { position: 0.3; color: "transparent" }
                        GradientStop { position: 0.7; color: "transparent" }
                        GradientStop { position: 1; color: Qt.rgba(0, 0, 0, 0.55) } // iris-literal: vignette falloff
                    }
                }
            }
            Row {
                anchors.centerIn: parent
                spacing: Math.round(18 * root.d)
                Repeater {
                    model: 3
                    Rectangle {
                        required property int index
                        width: Math.round((index === 1 ? 200 : 150) * root.d)
                        height: Math.round((index === 1 ? 130 : 100) * root.d)
                        anchors.verticalCenter: parent.verticalCenter
                        radius: IrisStyle.radiusTile
                        color: IrisStyle.surfaceHigh
                        border.width: index === 1 ? 2 : 1
                        border.color: index === 1 ? IrisStyle.accent : IrisStyle.border
                        Rectangle { x: 10; y: 10; width: parent.width * 0.5; height: 7; radius: 3.5; color: IrisStyle.fill }
                        Rectangle { x: 10; y: 24; width: parent.width * 0.7; height: 7; radius: 3.5; color: IrisStyle.fillQuiet }
                    }
                }
            }
            Caption {
                glyph: "grid_view"
                text: backdropRoot.on ? Translation.tr("Overview over your wallpaper") : Translation.tr("Overview over Niri's plain background")
            }
        }
    }

    Component {
        id: galleryScene
        Item {
            id: galleryRoot
            readonly property real galleryWidth: Math.max(640, Math.min(1400, Number(root.opt("iris.wallpaper.width", 960)))) * root.d
            readonly property real thumb: Math.max(160, Math.min(320, Number(root.opt("iris.wallpaper.thumbnailSize", 228)))) * root.d
            readonly property bool live: root.opt("iris.wallpaper.livePreview", true)
            readonly property int perRow: Math.max(1, Math.floor((galleryRoot.galleryWidth - 30 * root.d) / (galleryRoot.thumb + 10 * root.d)))
            readonly property var sources: {
                const list = Array.from(Wallpapers.wallpapers ?? []).map(path => "file://" + path)
                const pool = list.length > 0 ? list : [root.wallpaper]
                return Array.from({ length: galleryRoot.perRow * 2 }, (_, i) => pool[i % pool.length])
            }
            readonly property real naturalWidth: galleryRoot.galleryWidth + Math.round(80 * root.d)
            readonly property real naturalHeight: galleryRoot.thumb * 0.62 * 2 + Math.round(170 * root.d)
            property int focusIndex: 1
            Timer {
                running: root.playing
                interval: 1600
                repeat: true
                onTriggered: galleryRoot.focusIndex = (galleryRoot.focusIndex + 1) % Math.max(1, galleryRoot.sources.length)
            }
            Image {
                anchors.fill: parent
                visible: galleryRoot.live
                source: galleryRoot.sources[galleryRoot.focusIndex] ?? ""
                fillMode: Image.PreserveAspectCrop
                sourceSize.width: 700
                asynchronous: true
            }
            Plate {
                id: galleryPlate
                surface: "gallery"
                own: IrisStyle.wallpaperLight
                anchors.horizontalCenter: parent.horizontalCenter
                anchors.bottom: parent.bottom
                anchors.bottomMargin: Math.round(26 * root.d)
                width: galleryRoot.galleryWidth
                height: galleryFlow.implicitHeight + Math.round(64 * root.d)
                IrisText {
                    x: Math.round(20 * root.d); y: Math.round(16 * root.d)
                    text: Translation.tr("Wallpapers")
                    font.family: IrisStyle.fontTitle; font.weight: Font.Bold; font.pixelSize: 16 * IrisStyle.typeScale
                }
                Flow {
                    id: galleryFlow
                    x: Math.round(20 * root.d); y: Math.round(48 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(10 * root.d)
                    Repeater {
                        model: galleryRoot.sources
                        ClippingRectangle {
                            id: shot
                            required property string modelData
                            required property int index
                            width: galleryRoot.thumb
                            height: Math.round(galleryRoot.thumb * 0.62)
                            radius: IrisStyle.radiusTile
                            border.width: shot.index === galleryRoot.focusIndex ? 2 : 0
                            border.color: IrisStyle.accent
                            Image { anchors.fill: parent; source: shot.modelData; fillMode: Image.PreserveAspectCrop; sourceSize.width: 320; asynchronous: true }
                        }
                    }
                }
            }
            Caption {
                anchors.bottom: undefined
                anchors.top: parent.top
                glyph: galleryRoot.live ? "preview" : "preview_off"
                text: galleryRoot.live ? Translation.tr("The desktop previews the chosen wallpaper") : Translation.tr("Wallpapers apply only when chosen")
            }
        }
    }

    Component {
        id: spotlightScene
        Item {
            id: spotRoot
            readonly property bool on: root.opt("iris.modules.palette", true)
            readonly property bool fromIsland: String(root.opt("iris.palette.opens", "floating")) === "island"
            readonly property bool hints: root.opt("iris.palette.showHints", true)
            readonly property int results: Math.max(3, Math.min(14, Number(root.opt("iris.palette.maxResults", 8))))
            readonly property real plateWidth: Math.max(420, Math.min(900, Number(root.opt("iris.palette.width", 640)))) * root.d
            readonly property var apps: (TaskbarApps.apps ?? []).filter(app => app && !app.separator && String(app.appId ?? "").length > 0 && app.appId !== "SEPARATOR")
            readonly property real naturalWidth: spotRoot.plateWidth + Math.round(80 * root.d)
            readonly property real naturalHeight: spotPlate.y + spotPlate.height + Math.round(24 * root.d)
            readonly property bool cropBottom: true
            IslandPill {
                id: spotIsland
                visible: spotRoot.fromIsland
                anchors.horizontalCenter: parent.horizontalCenter
                y: IrisFrame.band
            }
            Plate {
                id: spotPlate
                surface: "spotlight"
                opacity: spotRoot.on ? 1 : 0.35
                anchors.horizontalCenter: parent.horizontalCenter
                y: spotRoot.fromIsland ? spotIsland.y + spotIsland.height + IrisStyle.weld : Math.round(40 * root.d)
                width: spotRoot.plateWidth
                height: spotColumn.implicitHeight + Math.round(28 * root.d)
                ColumnLayout {
                    id: spotColumn
                    x: Math.round(14 * root.d); y: Math.round(14 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(6 * root.d)
                    Rectangle {
                        Layout.fillWidth: true
                        implicitHeight: Math.round(40 * root.d)
                        radius: height / 2
                        color: IrisStyle.fillQuiet
                        RowLayout {
                            anchors.fill: parent
                            anchors.leftMargin: Math.round(14 * root.d)
                            spacing: Math.round(8 * root.d)
                            MaterialSymbol { text: "search"; iconSize: Math.round(18 * root.d); color: IrisStyle.muted }
                            IrisText { text: Translation.tr("Search apps, files and actions"); color: IrisStyle.muted; font.pixelSize: 13.5 * IrisStyle.typeScale }
                        }
                    }
                    Row {
                        visible: spotRoot.hints
                        spacing: Math.round(6 * root.d)
                        Repeater {
                            model: [";  " + Translation.tr("Clipboard"), "=  " + Translation.tr("Calculator"), "/  " + Translation.tr("Actions"), ":  " + Translation.tr("Emoji")]
                            Rectangle {
                                required property string modelData
                                width: hintLabel.implicitWidth + Math.round(18 * root.d)
                                height: Math.round(24 * root.d)
                                radius: height / 2
                                color: IrisStyle.fillQuiet
                                IrisText { id: hintLabel; anchors.centerIn: parent; text: parent.modelData; color: IrisStyle.subtext; font.pixelSize: 11 * IrisStyle.typeScale }
                            }
                        }
                    }
                    IrisText { text: Translation.tr("Top hit"); role: IrisText.Meta; Layout.topMargin: Math.round(4 * root.d) }
                    Repeater {
                        model: spotRoot.results
                        RowLayout {
                            id: hit
                            required property int index
                            readonly property var app: spotRoot.apps[hit.index % Math.max(1, spotRoot.apps.length)] ?? null
                            Layout.fillWidth: true
                            spacing: Math.round(10 * root.d)
                            Rectangle {
                                Layout.fillWidth: true
                                implicitHeight: Math.round(36 * root.d)
                                radius: IrisStyle.radiusRow
                                color: hit.index === 0 ? IrisStyle.tintFill(IrisStyle.accent) : "transparent"
                                RowLayout {
                                    anchors.fill: parent
                                    anchors.leftMargin: Math.round(8 * root.d)
                                    anchors.rightMargin: Math.round(10 * root.d)
                                    spacing: Math.round(10 * root.d)
                                    SmartAppIcon { implicitSize: Math.round(24 * root.d); icon: IrisPieces.appIcon(hit.app?.appId ?? ""); fallback: "application-x-executable" }
                                    IrisText {
                                        Layout.fillWidth: true
                                        text: DesktopEntries.heuristicLookup(hit.app?.appId ?? "")?.name ?? String(hit.app?.appId ?? Translation.tr("Result"))
                                        elide: Text.ElideRight
                                    }
                                    IrisText { text: hit.index === 0 ? Translation.tr("Open") : Translation.tr("App"); color: IrisStyle.muted; font.pixelSize: 11.5 * IrisStyle.typeScale }
                                }
                            }
                        }
                    }
                }
            }
            OffState { visible: !spotRoot.on; text: Translation.tr("Spotlight off") }
            Caption {
                glyph: spotRoot.fromIsland ? "pill" : "web_asset"
                text: Translation.tr("%1 results").arg(spotRoot.results) + " · " + (spotRoot.fromIsland ? Translation.tr("grows out of the Island") : Translation.tr("floats over the screen"))
            }
        }
    }

    Component {
        id: controlScene
        Item {
            id: ccRoot
            readonly property bool on: root.opt("iris.modules.controlCenter", true)
            readonly property bool fromIsland: String(root.opt("iris.controlCenter.opens", "island")) === "island"
            readonly property bool round: String(root.opt("iris.controlCenter.controls", "tiles")) === "round"
            readonly property real plateWidth: Math.max(320, Math.min(540, Number(root.opt("iris.controlCenter.width", 360)))) * root.d
            readonly property var sections: {
                const list = root.opt("iris.controlCenter.sections", ["connectivity", "media", "shortcuts", "levels", "notifications"])
                return Array.from(list ?? [])
            }
            readonly property var rows: {
                const out = []
                const pairs = [["connectivity", "media"], ["shortcuts", "levels"]]
                for (let i = 0; i < ccRoot.sections.length; i++) {
                    const a = ccRoot.sections[i], b = ccRoot.sections[i + 1]
                    if (b && pairs.some(p => (p[0] === a && p[1] === b) || (p[0] === b && p[1] === a))) { out.push([a, b]); i++ }
                    else out.push([a])
                }
                return out
            }
            readonly property real naturalWidth: ccRoot.plateWidth + Math.round(120 * root.d)
            readonly property real naturalHeight: ccPlate.y + ccPlate.height + Math.round(24 * root.d)
            readonly property bool cropBottom: true
            IslandPill {
                id: ccIsland
                anchors.horizontalCenter: parent.horizontalCenter
                y: IrisFrame.band
                opacity: ccRoot.fromIsland ? 0 : 1
            }
            Plate {
                id: ccPlate
                surface: "controlCenter"
                fallbackRadius: IrisStyle.radiusPanel
                opacity: ccRoot.on ? 1 : 0.35
                anchors.horizontalCenter: parent.horizontalCenter
                y: ccRoot.fromIsland ? IrisFrame.band : ccIsland.y + ccIsland.height + IrisFrame.bodyAir
                width: ccRoot.plateWidth
                height: ccColumn.implicitHeight + Math.round(28 * root.d)
                ColumnLayout {
                    id: ccColumn
                    x: Math.round(14 * root.d); y: Math.round(14 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(10 * root.d)
                    IrisText { text: Translation.tr("Control Center"); font.weight: Font.DemiBold; font.pixelSize: 15 * IrisStyle.typeScale }
                    Repeater {
                        model: ccRoot.rows
                        RowLayout {
                            id: ccRow
                            required property var modelData
                            Layout.fillWidth: true
                            spacing: Math.round(10 * root.d)
                            Repeater {
                                model: ccRow.modelData
                                Rectangle {
                                    id: block
                                    required property string modelData
                                    Layout.fillWidth: true
                                    Layout.preferredWidth: block.modelData === "levels" ? Math.round(150 * root.d) : 1
                                    Layout.maximumWidth: block.modelData === "levels" ? Math.round(150 * root.d) : Number.POSITIVE_INFINITY
                                    implicitHeight: block.modelData === "notifications" ? Math.round(52 * root.d) : Math.round(112 * root.d)
                                    radius: IrisStyle.radiusCard
                                    color: block.modelData === "levels" ? "transparent" : IrisStyle.surfaceHigh
                                    Grid {
                                        anchors.centerIn: parent
                                        visible: block.modelData === "connectivity" || block.modelData === "shortcuts"
                                        columns: block.modelData === "connectivity" ? 2 : 3
                                        spacing: Math.round(8 * root.d)
                                        Repeater {
                                            model: block.modelData === "connectivity" ? ["lan", "bluetooth", "do_not_disturb_off", "sports_esports"] : ["contrast", "nightlight", "coffee", "screenshot_region", "radio_button_checked", "speaker"]
                                            Rectangle {
                                                required property string modelData
                                                required property int index
                                                readonly property bool isRound: block.modelData === "connectivity" || ccRoot.round
                                                width: isRound ? Math.round(36 * root.d) : Math.round(44 * root.d)
                                                height: isRound ? width : Math.round(38 * root.d)
                                                radius: isRound ? width / 2 : IrisStyle.radiusTile
                                                color: index === 0 ? IrisStyle.accent : IrisStyle.fill
                                                MaterialSymbol { anchors.centerIn: parent; text: parent.modelData; iconSize: Math.round(17 * root.d); color: parent.index === 0 ? IrisStyle.onAccent : IrisStyle.text }
                                            }
                                        }
                                    }
                                    ColumnLayout {
                                        anchors.fill: parent
                                        anchors.margins: Math.round(12 * root.d)
                                        visible: block.modelData === "media"
                                        MaterialSymbol { text: "music_note"; iconSize: Math.round(20 * root.d); color: IrisStyle.subtext }
                                        Item { Layout.fillHeight: true }
                                        IrisText { Layout.fillWidth: true; text: MprisController.activePlayer?.trackTitle || Translation.tr("Not playing"); font.weight: Font.DemiBold; elide: Text.ElideRight }
                                        IrisText { Layout.fillWidth: true; text: MprisController.activePlayer?.trackArtist || Translation.tr("Music will show here"); color: IrisStyle.subtext; font.pixelSize: 11 * IrisStyle.typeScale; elide: Text.ElideRight }
                                    }
                                    Row {
                                        anchors.fill: parent
                                        visible: block.modelData === "levels"
                                        spacing: Math.round(8 * root.d)
                                        Repeater {
                                            model: [["light_mode", 0.45], ["volume_up", 0.7], ["mic", 1]]
                                            Rectangle {
                                                required property var modelData
                                                width: Math.round(44 * root.d); height: parent.height
                                                radius: width / 2
                                                color: IrisStyle.fill
                                                Rectangle { anchors.bottom: parent.bottom; width: parent.width; height: parent.height * parent.modelData[1]; radius: width / 2; color: IrisStyle.fillStrong }
                                                MaterialSymbol { anchors.horizontalCenter: parent.horizontalCenter; anchors.bottom: parent.bottom; anchors.bottomMargin: 8; text: parent.modelData[0]; iconSize: Math.round(15 * root.d); color: IrisStyle.surface }
                                            }
                                        }
                                    }
                                    RowLayout {
                                        anchors.fill: parent
                                        anchors.margins: Math.round(12 * root.d)
                                        visible: block.modelData === "notifications"
                                        MaterialSymbol { text: "notifications"; iconSize: Math.round(17 * root.d); color: IrisStyle.subtext }
                                        IrisText { Layout.fillWidth: true; text: Translation.tr("You're all caught up"); color: IrisStyle.subtext }
                                    }
                                }
                            }
                        }
                    }
                }
            }
            OffState { visible: !ccRoot.on; text: Translation.tr("Control Center off") }
            Caption {
                glyph: ccRoot.fromIsland ? "pill" : "web_asset"
                text: (ccRoot.fromIsland ? Translation.tr("Becomes the Island's controls page") : Translation.tr("Hangs from the Island as a panel")) + " · " + (ccRoot.round ? Translation.tr("round") : Translation.tr("tiles"))
            }
        }
    }

    Component {
        id: cardsScene
        Item {
            id: cardRoot
            readonly property real cardWidth: Number(root.opt("iris.appearance.surfaces.cards.width", 0)) > 0
                ? Number(root.opt("iris.appearance.surfaces.cards.width", 0)) * root.d : Math.round(340 * root.d)
            readonly property bool header: root.opt("iris.appearance.surfaces.cards.header", true)
            readonly property bool devices: root.opt("iris.appearance.surfaces.cards.devices", true)
            readonly property bool mixer: root.opt("iris.appearance.surfaces.cards.mixer", true)
            readonly property real naturalWidth: cardRoot.cardWidth + Math.round(120 * root.d)
            readonly property real naturalHeight: cardPlate.y + cardPlate.height + Math.round(24 * root.d)
            Rectangle {
                id: origin
                anchors.horizontalCenter: parent.horizontalCenter
                y: IrisFrame.band
                width: IrisFrame.islandBand; height: width
                radius: IrisStyle.pieceRadius(width)
                color: IrisStyle.bodySurface
                MaterialSymbol { anchors.centerIn: parent; text: "volume_up"; fill: 1; iconSize: Math.round(18 * root.d); color: IrisStyle.text }
            }
            Plate {
                id: cardPlate
                surface: "cards"
                own: IrisStyle.identity.sky
                anchors.horizontalCenter: parent.horizontalCenter
                y: origin.y + origin.height + IrisStyle.weld
                width: cardRoot.cardWidth
                height: cardColumn.implicitHeight + Math.round(32 * root.d)
                ColumnLayout {
                    id: cardColumn
                    x: Math.round(16 * root.d); y: Math.round(16 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(12 * root.d)
                    RowLayout {
                        visible: cardRoot.header
                        spacing: Math.round(10 * root.d)
                        Rectangle {
                            implicitWidth: Math.round(30 * root.d); implicitHeight: implicitWidth
                            radius: IrisStyle.iconRadius(width); color: IrisStyle.identity.sky
                            MaterialSymbol { anchors.centerIn: parent; text: "volume_up"; fill: 1; iconSize: Math.round(16 * root.d); color: IrisStyle.onTint }
                        }
                        ColumnLayout {
                            spacing: 0
                            IrisText { text: Translation.tr("Sound"); font.weight: Font.DemiBold }
                            IrisText { text: Audio.sink?.description ?? Translation.tr("Speakers"); color: IrisStyle.subtext; font.pixelSize: 11.5 * IrisStyle.typeScale }
                        }
                    }
                    RowLayout {
                        Layout.fillWidth: true
                        spacing: Math.round(10 * root.d)
                        MaterialSymbol { text: "volume_up"; iconSize: Math.round(18 * root.d); color: IrisStyle.text }
                        Level { value: Math.min(1, Audio.value ?? 0.6); tint: IrisStyle.text }
                        IrisText { text: Math.round(Math.min(1, Audio.value ?? 0.6) * 100); font.family: IrisStyle.fontNumbers; font.weight: Font.DemiBold }
                    }
                    Row {
                        visible: cardRoot.devices
                        spacing: Math.round(6 * root.d)
                        Repeater {
                            model: [Translation.tr("Speakers"), Translation.tr("Headphones")]
                            Rectangle {
                                required property string modelData
                                required property int index
                                width: devLabel.implicitWidth + Math.round(20 * root.d); height: Math.round(26 * root.d)
                                radius: height / 2
                                color: index === 0 ? IrisStyle.tintFill(IrisStyle.accent) : IrisStyle.fillQuiet
                                IrisText { id: devLabel; anchors.centerIn: parent; text: parent.modelData; color: parent.index === 0 ? IrisStyle.accent : IrisStyle.subtext; font.pixelSize: 11.5 * IrisStyle.typeScale }
                            }
                        }
                    }
                    Repeater {
                        model: cardRoot.mixer ? (TaskbarApps.apps ?? []).filter(app => app && !app.separator && String(app.appId ?? "").length > 0 && app.appId !== "SEPARATOR").slice(0, 2) : []
                        RowLayout {
                            required property var modelData
                            required property int index
                            Layout.fillWidth: true
                            spacing: Math.round(10 * root.d)
                            SmartAppIcon { implicitSize: Math.round(20 * root.d); icon: IrisPieces.appIcon(parent.modelData.appId); fallback: "application-x-executable" }
                            Level { value: parent.index === 0 ? 0.8 : 0.45 }
                        }
                    }
                }
            }
        }
    }

    Component {
        id: menusScene
        Item {
            id: menuRoot
            readonly property real naturalWidth: Math.round(480 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            Plate {
                surface: "menus"
                fallbackRadius: IrisStyle.radiusCard
                anchors.centerIn: parent
                width: Math.round(260 * root.d)
                height: menuColumn.implicitHeight + Math.round(12 * root.d)
                ColumnLayout {
                    id: menuColumn
                    x: Math.round(6 * root.d); y: Math.round(6 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(2 * root.d)
                    RowLayout {
                        Layout.fillWidth: true
                        spacing: Math.round(6 * root.d)
                        Repeater {
                            model: [["wallpaper", Translation.tr("Wallpaper")], ["widgets", Translation.tr("Widgets")], ["palette", Translation.tr("Studio")]]
                            Rectangle {
                                required property var modelData
                                Layout.fillWidth: true
                                implicitHeight: Math.round(58 * root.d)
                                radius: IrisStyle.radiusTile
                                color: IrisStyle.fillQuiet
                                ColumnLayout {
                                    anchors.centerIn: parent
                                    spacing: Math.round(3 * root.d)
                                    MaterialSymbol { Layout.alignment: Qt.AlignHCenter; text: parent.parent.modelData[0]; fill: 1; iconSize: Math.round(19 * root.d); color: IrisStyle.text }
                                    IrisText { Layout.alignment: Qt.AlignHCenter; text: parent.parent.modelData[1]; font.pixelSize: 11 * IrisStyle.typeScale; font.weight: Font.DemiBold }
                                }
                            }
                        }
                    }
                    Rectangle { Layout.fillWidth: true; Layout.margins: Math.round(6 * root.d); implicitHeight: 1; color: IrisStyle.hairlineStrong }
                    Repeater {
                        model: [["edit", Translation.tr("Edit iRiS")], ["tune", Translation.tr("Quick controls")], ["settings", Translation.tr("Settings")]]
                        Rectangle {
                            required property var modelData
                            required property int index
                            Layout.fillWidth: true
                            implicitHeight: Math.round(34 * root.d)
                            radius: IrisStyle.radiusRow
                            color: index === 0 ? IrisStyle.tintFill(IrisStyle.accent) : "transparent"
                            RowLayout {
                                anchors.fill: parent
                                anchors.leftMargin: Math.round(10 * root.d)
                                spacing: Math.round(10 * root.d)
                                MaterialSymbol { Layout.preferredWidth: Math.round(20 * root.d); text: parent.parent.modelData[0]; iconSize: Math.round(17 * root.d); color: IrisStyle.subtext }
                                IrisText { text: parent.parent.modelData[1]; font.weight: Font.Medium }
                            }
                        }
                    }
                }
            }
        }
    }

    Component {
        id: settingsScene
        Item {
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            Plate {
                surface: "settings"
                fallbackRadius: IrisStyle.radiusPanel
                anchors.centerIn: parent
                width: Math.round(540 * root.d)
                height: Math.round(250 * root.d)
                clip: true
                Rectangle {
                    x: 1; y: 1
                    width: Math.round(140 * root.d); height: parent.height - 2
                    radius: parent.radius
                    color: IrisStyle.surfaceHigh
                    Column {
                        x: Math.round(12 * root.d); y: Math.round(16 * root.d)
                        spacing: Math.round(8 * root.d)
                        Repeater {
                            model: [IrisStyle.identity.blue, IrisStyle.identity.pink, IrisStyle.identity.sky, IrisStyle.identity.indigo, IrisStyle.identity.purple]
                            Row {
                                required property color modelData
                                spacing: Math.round(8 * root.d)
                                Rectangle { width: Math.round(18 * root.d); height: width; radius: IrisStyle.iconRadius(width); color: parent.modelData }
                                Rectangle { anchors.verticalCenter: parent.verticalCenter; width: Math.round(70 * root.d); height: Math.round(7 * root.d); radius: height / 2; color: IrisStyle.fill }
                            }
                        }
                    }
                }
                Grid {
                    x: Math.round(160 * root.d); y: Math.round(20 * root.d)
                    columns: 3
                    spacing: Math.round(10 * root.d)
                    Repeater {
                        model: 6
                        Rectangle {
                            required property int index
                            width: Math.round(110 * root.d); height: Math.round(92 * root.d)
                            radius: IrisStyle.radiusCard
                            color: IrisStyle.surfaceHigh
                            border.width: 1
                            border.color: IrisStyle.rim
                            Rectangle { x: 10; y: 10; width: Math.round(20 * root.d); height: width; radius: IrisStyle.iconRadius(width); color: IrisStyle.identity.blue }
                            Rectangle { x: 10; y: parent.height - 30; width: parent.width * 0.55; height: 7; radius: 3.5; color: IrisStyle.fillStrong }
                            Rectangle { x: 10; y: parent.height - 17; width: parent.width * 0.75; height: 6; radius: 3; color: IrisStyle.fill }
                        }
                    }
                }
            }
        }
    }

    Component {
        id: panelsScene
        Item {
            id: panelRoot
            readonly property real panelWidth: Math.max(300, Math.min(560, Number(root.opt("iris.sidebars.right.width", 380)))) * root.d
            readonly property real naturalWidth: panelRoot.panelWidth * 2.2
            readonly property real naturalHeight: Math.round(420 * root.d)
            Plate {
                surface: "panels"
                fallbackRadius: IrisStyle.radiusPanel
                anchors.right: parent.right
                anchors.rightMargin: IrisFrame.band + IrisFrame.bodyAir
                y: IrisFrame.band + IrisFrame.bodyAir
                width: panelRoot.panelWidth
                height: parent.height - 2 * y
                ColumnLayout {
                    anchors.fill: parent
                    anchors.margins: Math.round(16 * root.d)
                    spacing: Math.round(10 * root.d)
                    RowLayout {
                        spacing: Math.round(10 * root.d)
                        IrisText { text: Qt.locale().toString(DateTime.clock.date, "d"); color: IrisStyle.identity.red; font.family: IrisStyle.fontNumbers; font.weight: Font.Bold; font.pixelSize: 30 * IrisStyle.typeScale }
                        ColumnLayout {
                            spacing: 0
                            IrisText { text: Translation.tr("Today"); font.family: IrisStyle.fontTitle; font.weight: Font.Bold; font.pixelSize: 17 * IrisStyle.typeScale }
                            IrisText { text: Qt.locale().toString(DateTime.clock.date, "dddd, MMMM"); color: IrisStyle.subtext; font.pixelSize: 11.5 * IrisStyle.typeScale }
                        }
                    }
                    Repeater {
                        model: [["calendar_month", Translation.tr("Calendar"), IrisStyle.identity.red], ["partly_cloudy_day", Translation.tr("Weather"), IrisStyle.identity.sky], ["notifications", Translation.tr("Notifications"), IrisStyle.identity.orange]]
                        Rectangle {
                            required property var modelData
                            Layout.fillWidth: true
                            Layout.fillHeight: true
                            radius: IrisStyle.radiusCard
                            color: IrisStyle.surfaceHigh
                            RowLayout {
                                x: Math.round(12 * root.d); y: Math.round(12 * root.d)
                                spacing: Math.round(8 * root.d)
                                Rectangle {
                                    implicitWidth: Math.round(24 * root.d); implicitHeight: implicitWidth
                                    radius: IrisStyle.iconRadius(width); color: parent.parent.modelData[2]
                                    MaterialSymbol { anchors.centerIn: parent; text: parent.parent.parent.modelData[0]; fill: 1; iconSize: Math.round(14 * root.d); color: IrisStyle.onTint }
                                }
                                IrisText { text: parent.parent.modelData[1]; font.weight: Font.DemiBold }
                            }
                        }
                    }
                }
            }
        }
    }

    Component {
        id: joiningScene
        Item {
            id: joinRoot
            readonly property real naturalWidth: Math.round(640 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            readonly property bool along: String(root.opt("iris.appearance.theme.placement", "auto")) === "along"
            readonly property real air: IrisFrame.bodyAir
            readonly property real bubble: IrisFrame.islandBand
            readonly property var island: ({ x: Math.round(width / 2 - 170 * root.d), y: IrisFrame.band, width: Math.round(150 * root.d), height: joinRoot.bubble })
            readonly property var origin: ({ x: joinRoot.island.x + joinRoot.island.width + Math.round(6 * root.d), y: IrisFrame.band, width: joinRoot.bubble, height: joinRoot.bubble })
            readonly property var card: joinRoot.along
                ? ({ x: joinRoot.origin.x + joinRoot.origin.width + IrisStyle.weld, y: IrisFrame.band, width: Math.round(200 * root.d), height: Math.round(130 * root.d) })
                : ({ x: joinRoot.origin.x + joinRoot.origin.width / 2 - Math.round(100 * root.d), y: joinRoot.origin.y + joinRoot.origin.height + IrisStyle.weld, width: Math.round(200 * root.d), height: Math.round(130 * root.d) })
            readonly property var neighbour: ({ x: joinRoot.card.x + joinRoot.card.width + joinRoot.air, y: joinRoot.card.y + (joinRoot.along ? 0 : Math.round(20 * root.d)), width: Math.round(120 * root.d), height: Math.round(100 * root.d) })
            Field.IrisField {
                anchors.fill: parent
                framed: false
                shapes: {
                    const deep = Math.max(8, IrisStyle.fuseDeep * 2)
                    const sheet = IrisStyle.radiusSheet
                    return [
                        { x: -2 * IrisStyle.fuseDeep, y: -deep - 1 + IrisFrame.band, width: joinRoot.width + 4 * IrisStyle.fuseDeep, height: deep, radius: 0, fuse: IrisStyle.fuseDeep, id: "edge", paints: true },
                        Object.assign({ radius: joinRoot.bubble / 2, fuse: IrisStyle.fuseEdge, id: "island", joins: "edge", paints: true }, joinRoot.island),
                        Object.assign({ radius: IrisStyle.pieceRadius(joinRoot.bubble), fuse: IrisStyle.fuse, id: "origin", joins: "island", paints: true }, joinRoot.origin),
                        Object.assign({ radius: sheet, fuse: IrisStyle.fuse, id: "card", joins: "origin", paints: true }, joinRoot.card),
                        Object.assign({ radius: sheet, fuse: IrisStyle.fuse, id: "neighbour", paints: true }, joinRoot.neighbour)
                    ]
                }
            }
            MaterialSymbol { x: joinRoot.origin.x + (joinRoot.origin.width - width) / 2; y: joinRoot.origin.y + (joinRoot.origin.height - height) / 2; text: "partly_cloudy_day"; fill: 1; iconSize: Math.round(18 * root.d); color: IrisStyle.identity.sky }
            IrisClock { x: joinRoot.island.x + (joinRoot.island.width - width) / 2; y: joinRoot.island.y + (joinRoot.island.height - height) / 2; pixelSize: 15 * IrisStyle.typeScale; separatorColor: IrisStyle.secondaryAccent }
            Rectangle {
                visible: joinRoot.air > 0
                x: joinRoot.card.x + joinRoot.card.width
                y: joinRoot.neighbour.y + joinRoot.neighbour.height / 2
                width: joinRoot.air; height: 2
                color: IrisStyle.accent
            }
            Caption {
                glyph: joinRoot.along ? "swap_horiz" : "south"
                text: (joinRoot.along ? Translation.tr("Opens along the edge") : Translation.tr("Opens away from the edge"))
                    + " · " + Translation.tr("air %1 px").arg(Math.round(joinRoot.air / root.d))
            }
        }
    }

    Component {
        id: feedbackScene
        Item {
            id: feedRoot
            readonly property bool banners: root.opt("iris.modules.notificationPopup", true)
            readonly property bool osd: root.opt("iris.modules.osd", true)
            readonly property real bannerWidth: Math.max(340, Math.min(560, Number(root.opt("iris.notifications.width", 380)))) * root.d
            readonly property real osdWidth: Math.max(260, Math.min(520, Number(root.opt("iris.osd.width", 320)))) * root.d
            readonly property int duration: Math.max(2000, Math.min(12000, Number(root.opt("iris.notifications.duration", 4000))))
            readonly property real naturalWidth: Math.max(feedRoot.bannerWidth, feedRoot.osdWidth) + Math.round(120 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            IslandPill { id: feedIsland; anchors.horizontalCenter: parent.horizontalCenter; y: IrisFrame.band }
            Plate {
                id: bannerPlate
                surface: "cards"
                own: IrisStyle.identity.orange
                opacity: feedRoot.banners ? 1 : 0.3
                anchors.horizontalCenter: parent.horizontalCenter
                y: feedIsland.y + feedIsland.height + IrisStyle.weld
                width: feedRoot.bannerWidth
                height: Math.round(76 * root.d)
                radius: IrisStyle.radiusPlate
                RowLayout {
                    anchors.fill: parent
                    anchors.margins: Math.round(14 * root.d)
                    spacing: Math.round(12 * root.d)
                    IrisMark { implicitSize: Math.round(38 * root.d) }
                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: Math.round(2 * root.d)
                        IrisText { text: Translation.tr("A notification"); font.weight: Font.DemiBold }
                        IrisText { Layout.fillWidth: true; text: Translation.tr("Stays %1 s, then folds back into the Island.").arg((feedRoot.duration / 1000).toFixed(1)); color: IrisStyle.textSecondary; elide: Text.ElideRight }
                    }
                }
                Rectangle {
                    id: drain
                    anchors.bottom: parent.bottom
                    anchors.bottomMargin: Math.round(6 * root.d)
                    x: Math.round(18 * root.d)
                    height: 2
                    radius: 1
                    color: IrisStyle.secondaryAccent
                    readonly property real full: parent.width - 2 * x
                    width: drain.full
                    NumberAnimation on width {
                        running: root.playing && feedRoot.banners
                        loops: Animation.Infinite
                        from: drain.full; to: 0
                        duration: feedRoot.duration
                    }
                }
            }
            Rectangle {
                anchors.horizontalCenter: parent.horizontalCenter
                y: bannerPlate.y + bannerPlate.height + Math.round(40 * root.d)
                opacity: feedRoot.osd ? 1 : 0.3
                width: feedRoot.osdWidth
                height: Math.round(44 * root.d)
                radius: height / 2
                color: IrisStyle.bodySurface
                border.width: IrisStyle.rim.a > 0 ? 1 : 0
                border.color: IrisStyle.rim
                RowLayout {
                    anchors.fill: parent
                    anchors.leftMargin: Math.round(14 * root.d)
                    anchors.rightMargin: Math.round(16 * root.d)
                    spacing: Math.round(10 * root.d)
                    MaterialSymbol { text: "volume_up"; fill: 1; iconSize: Math.round(18 * root.d); color: IrisStyle.text }
                    Level { value: Math.min(1, Audio.value ?? 0.7); tint: IrisStyle.text }
                    IrisText { text: Math.round(Math.min(1, Audio.value ?? 0.7) * 100); font.family: IrisStyle.fontNumbers; font.weight: Font.DemiBold }
                }
            }
            Caption {
                glyph: "campaign"
                text: [feedRoot.banners ? Translation.tr("Banners on") : Translation.tr("Banners off"), feedRoot.osd ? Translation.tr("level feedback on") : Translation.tr("level feedback off")].join(" · ")
            }
        }
    }

    Component {
        id: trayScene
        Item {
            id: trayRoot
            readonly property int columns: Math.max(2, Math.min(6, Number(root.opt("iris.tray.columns", 4))))
            readonly property bool labels: root.opt("iris.tray.labels", true)
            readonly property bool hidePassive: root.opt("iris.tray.hidePassive", false)
            readonly property var items: (SystemTray.items.values ?? []).filter(item => !trayRoot.hidePassive || item.status !== Status.Passive)
            readonly property real naturalWidth: trayPlate.width + Math.round(80 * root.d)
            readonly property real naturalHeight: trayPlate.height + Math.round(80 * root.d)
            Plate {
                id: trayPlate
                surface: "cards"
                anchors.centerIn: parent
                width: trayGrid.implicitWidth + Math.round(32 * root.d)
                height: trayGrid.implicitHeight + Math.round(64 * root.d)
                IrisText {
                    x: Math.round(16 * root.d); y: Math.round(14 * root.d)
                    text: Translation.tr("Tray") + "  " + trayRoot.items.length
                    font.weight: Font.DemiBold
                }
                Grid {
                    id: trayGrid
                    x: Math.round(16 * root.d); y: Math.round(46 * root.d)
                    columns: trayRoot.columns
                    spacing: Math.round(6 * root.d)
                    Repeater {
                        model: trayRoot.items.length > 0 ? trayRoot.items : [null, null, null]
                        Column {
                            id: trayEntry
                            required property var modelData
                            width: Math.round(72 * root.d)
                            spacing: Math.round(5 * root.d)
                            Rectangle {
                                anchors.horizontalCenter: parent.horizontalCenter
                                width: Math.round(48 * root.d); height: width
                                radius: IrisStyle.iconRadius(width)
                                color: IrisStyle.fillQuiet
                                IconImage { anchors.centerIn: parent; implicitSize: Math.round(26 * root.d); source: trayEntry.modelData ? TrayService.getSafeIcon(trayEntry.modelData) : "" }
                            }
                            IrisText {
                                visible: trayRoot.labels
                                width: parent.width
                                horizontalAlignment: Text.AlignHCenter
                                text: trayEntry.modelData?.tooltipTitle || trayEntry.modelData?.title || trayEntry.modelData?.id || Translation.tr("App")
                                elide: Text.ElideRight
                                font.pixelSize: 11 * IrisStyle.typeScale
                                color: IrisStyle.subtext
                            }
                        }
                    }
                }
            }
            Caption {
                glyph: "inventory_2"
                text: Translation.tr("%1 columns").arg(trayRoot.columns) + (trayRoot.hidePassive ? " · " + Translation.tr("passive apps hidden") : "")
            }
        }
    }

    Component {
        id: playerScene
        Item {
            id: playerRoot
            readonly property var player: MprisController.activePlayer
            readonly property string art: String(playerRoot.player?.trackArtUrl ?? "")
            readonly property string cover: playerRoot.art.length > 0 ? playerRoot.art : root.wallpaper
            readonly property bool roundCover: root.opt("iris.player.roundCover", true)
            readonly property bool artBackground: root.opt("iris.player.artworkBackground", true)
            readonly property bool opensIsland: String(root.opt("iris.player.bubbleOpens", "card")) === "island"
            readonly property bool pinned: root.opt("iris.player.cardPinned", false)
            readonly property real naturalWidth: Math.round(560 * root.d)
            readonly property real naturalHeight: Math.round(290 * root.d)
            ClippingRectangle {
                id: playerCard
                anchors.centerIn: parent
                width: Math.round(380 * root.d)
                height: Math.round(210 * root.d)
                radius: playerRoot.opensIsland ? IrisStyle.radius : IrisStyle.radiusSheet
                color: IrisStyle.bodySurface
                border.width: IrisStyle.rim.a > 0 ? 1 : 0
                border.color: IrisStyle.rim
                Image {
                    id: artSource
                    anchors.fill: parent
                    anchors.margins: -40
                    source: playerRoot.cover
                    fillMode: Image.PreserveAspectCrop
                    sourceSize.width: 240
                    visible: false
                }
                MultiEffect {
                    anchors.fill: artSource
                    source: artSource
                    visible: playerRoot.artBackground
                    blurEnabled: true; blur: 1; blurMax: 48
                    brightness: -0.25
                }
                Rectangle { anchors.fill: parent; visible: playerRoot.artBackground; color: IrisStyle.mediaScrim }
                ColumnLayout {
                    anchors.fill: parent
                    anchors.margins: Math.round(18 * root.d)
                    spacing: Math.round(10 * root.d)
                    RowLayout {
                        spacing: Math.round(14 * root.d)
                        ClippingRectangle {
                            implicitWidth: Math.round(84 * root.d); implicitHeight: implicitWidth
                            radius: playerRoot.roundCover ? width / 2 : IrisStyle.radiusTile
                            Behavior on radius { NumberAnimation { duration: IrisStyle.morphDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.morphCurve } }
                            Image { anchors.fill: parent; source: playerRoot.cover; fillMode: Image.PreserveAspectCrop; sourceSize.width: 200 }
                        }
                        ColumnLayout {
                            Layout.fillWidth: true
                            spacing: Math.round(2 * root.d)
                            IrisText { Layout.fillWidth: true; text: playerRoot.player?.trackTitle || Translation.tr("Song title"); color: playerRoot.artBackground ? IrisStyle.onMedia : IrisStyle.text; font.weight: Font.DemiBold; font.pixelSize: 15 * IrisStyle.typeScale; elide: Text.ElideRight }
                            IrisText { Layout.fillWidth: true; text: playerRoot.player?.trackArtist || Translation.tr("Artist"); color: playerRoot.artBackground ? IrisStyle.onMediaSecondary : IrisStyle.subtext; elide: Text.ElideRight }
                        }
                        MaterialSymbol { visible: playerRoot.pinned; Layout.alignment: Qt.AlignTop; text: "keep"; fill: 1; iconSize: Math.round(18 * root.d); color: playerRoot.artBackground ? IrisStyle.onMedia : IrisStyle.accent }
                    }
                    Item { Layout.fillHeight: true }
                    Level { value: 0.38; tint: playerRoot.artBackground ? IrisStyle.onMedia : IrisStyle.text; color: playerRoot.artBackground ? IrisStyle.onMediaFill : IrisStyle.fill }
                    RowLayout {
                        Layout.alignment: Qt.AlignHCenter
                        spacing: Math.round(26 * root.d)
                        Repeater {
                            model: ["skip_previous", "pause", "skip_next"]
                            MaterialSymbol { required property string modelData; text: modelData; fill: 1; iconSize: Math.round(24 * root.d); color: playerRoot.artBackground ? IrisStyle.onMedia : IrisStyle.text }
                        }
                    }
                }
            }
            Caption {
                glyph: playerRoot.opensIsland ? "pill" : "web_asset"
                text: (playerRoot.opensIsland ? Translation.tr("The media bubble opens the Island") : Translation.tr("The media bubble opens a card"))
                    + (playerRoot.pinned ? " · " + Translation.tr("kept open") : "")
            }
        }
    }

    Component {
        id: islandPageScene
        Item {
            id: pageRoot
            readonly property string mode: root.group === "Player page" ? "media" : root.group === "Pages" ? "pages" : "desktop"
            readonly property real pageW: Math.max(360, Math.min(600, Number(root.opt("iris.bar.pageWidth", 440)))) * root.d
            readonly property bool banner: String(root.opt("iris.bar.desktopBanner", "wallpaper")) === "wallpaper"
            readonly property bool grouped: String(root.opt("iris.bar.blockStyle", "plain")) === "grouped"
            readonly property var desktopBlocks: Array.from(root.opt("iris.bar.desktopBlocks", ["profile", "context", "forecast", "agenda", "modules"]))
            readonly property var mediaBlocks: Array.from(root.opt("iris.bar.mediaBlocks", ["player", "timeline", "transport", "players", "levels"]))
            readonly property var navKinds: ["media", "activity", "desktop", "tray", "tools", "focus", "today", "controls", "settings"]
            readonly property var navEntries: {
                const chosen = Array.from(root.opt("iris.bar.navItems", pageRoot.navKinds)).filter(kind => pageRoot.navKinds.includes(kind))
                const glyphs = { media: "music_note", activity: "bolt", desktop: "space_dashboard", tray: "apps", tools: "timer",
                    focus: "left_panel_open", today: "right_panel_open", controls: "tune", settings: "settings" }
                const pageKinds = ["media", "activity", "desktop", "tray", "tools"]
                const out = []
                let pages = true
                for (const kind of (chosen.length > 0 ? chosen : pageRoot.navKinds)) {
                    const page = pageKinds.includes(kind)
                    if (pages && !page && out.length > 0) out.push({ kind: "|" })
                    if (!page) pages = false
                    out.push({ kind: kind, page: page, glyph: glyphs[kind] })
                }
                return out
            }
            readonly property int pageCount: pageRoot.navEntries.filter(entry => entry.page).length
            readonly property int actionCount: pageRoot.navEntries.filter(entry => entry.kind !== "|" && !entry.page).length
            readonly property string current: pageRoot.mode === "media" ? "media" : "desktop"
            readonly property var player: MprisController.activePlayer
            readonly property var hours: Array.from(Weather.data?.hourly ?? []).slice(0, 6)
            readonly property bool weatherReady: Weather.enabled && !String(Weather.data?.temp ?? "--").startsWith("--")
            readonly property var lastWindow: {
                const stamp = w => (w.focus_timestamp?.secs ?? 0) * 1e9 + (w.focus_timestamp?.nanos ?? 0)
                return (NiriService.windows ?? []).slice().sort((a, b) => stamp(b) - stamp(a))[0] ?? null
            }
            readonly property real naturalWidth: pageRoot.pageW + Math.round(160 * root.d)
            readonly property real naturalHeight: islandBody.y + islandBody.height + Math.round(56 * root.d)
            readonly property bool cropBottom: true

            Rectangle {
                id: islandBody
                anchors.horizontalCenter: parent.horizontalCenter
                y: -radius
                width: pageRoot.pageW
                height: radius + pageColumn.implicitHeight + Math.round(36 * root.d)
                radius: IrisStyle.radius
                color: IrisStyle.bodySurface
                border.width: IrisStyle.rim.a > 0 ? 1 : 0
                border.color: IrisStyle.rim
                clip: true
                Behavior on width { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }

                Image {
                    visible: pageRoot.mode !== "media" && pageRoot.banner
                    x: 0; y: islandBody.radius
                    width: parent.width
                    height: Math.min(Math.round(150 * root.d), islandBody.height - 2 * islandBody.radius)
                    source: root.wallpaper
                    fillMode: Image.PreserveAspectCrop
                    sourceSize.width: 600
                    Rectangle {
                        anchors.fill: parent
                        gradient: Gradient {
                            GradientStop { position: 0; color: IrisStyle.veilLight }
                            GradientStop { position: 1; color: IrisStyle.bodySurface }
                        }
                    }
                }

                ColumnLayout {
                    id: pageColumn
                    x: Math.round(20 * root.d)
                    y: islandBody.radius + Math.round(14 * root.d)
                    width: parent.width - 2 * x
                    spacing: Math.round(14 * root.d)

                    Row {
                        Layout.alignment: Qt.AlignHCenter
                        spacing: Math.round(6 * root.d)
                        Repeater {
                            model: pageRoot.navEntries
                            Item {
                                id: navEntry
                                required property var modelData
                                readonly property bool divider: navEntry.modelData.kind === "|"
                                readonly property bool selected: navEntry.modelData.kind === pageRoot.current
                                width: navEntry.divider ? Math.round(9 * root.d) : Math.round(32 * root.d)
                                height: Math.round(28 * root.d)
                                Rectangle {
                                    visible: navEntry.divider
                                    anchors.centerIn: parent
                                    width: 1; height: Math.round(16 * root.d)
                                    color: IrisStyle.hairlineStrong
                                }
                                Rectangle {
                                    visible: !navEntry.divider
                                    anchors.fill: parent
                                    radius: height / 2
                                    color: navEntry.selected ? IrisStyle.tintFill(IrisStyle.secondaryAccent) : pageRoot.mode === "pages" && navEntry.modelData.page ? IrisStyle.fillQuiet : "transparent"
                                    MaterialSymbol {
                                        anchors.centerIn: parent
                                        text: navEntry.modelData.glyph ?? ""
                                        fill: navEntry.selected ? 1 : 0
                                        iconSize: Math.round(17 * root.d)
                                        color: navEntry.selected ? IrisStyle.secondaryAccent : IrisStyle.subtext
                                    }
                                }
                            }
                        }
                    }

                    ColumnLayout {
                        visible: pageRoot.mode !== "media"
                        Layout.fillWidth: true
                        Layout.topMargin: pageRoot.banner ? Math.round(14 * root.d) : 0
                        spacing: Math.round(2 * root.d)
                        IrisText {
                            text: "<font color='" + IrisStyle.secondaryAccent + "'><b>" + Qt.locale().toString(DateTime.clock.date, "dddd") + "</b></font> " + Qt.locale().toString(DateTime.clock.date, "d MMMM")
                            textFormat: Text.StyledText
                            font.pixelSize: 13 * IrisStyle.typeScale
                        }
                        RowLayout {
                            Layout.fillWidth: true
                            IrisClock { pixelSize: 36 * IrisStyle.typeScale }
                            Item { Layout.fillWidth: true }
                            MaterialSymbol { visible: pageRoot.weatherReady; text: Icons.getWeatherIcon(Weather.data?.wCode, Weather.isNightNow()) ?? "cloud"; fill: 1; iconSize: Math.round(22 * root.d); color: IrisStyle.text }
                            IrisText { visible: pageRoot.weatherReady; text: String(Weather.data?.temp ?? ""); font.family: IrisStyle.fontNumbers; font.weight: IrisStyle.figureWeight; font.pixelSize: 22 * IrisStyle.typeScale }
                        }
                    }

                    Repeater {
                        model: pageRoot.mode === "desktop" ? pageRoot.desktopBlocks : []
                        Loader {
                            id: desktopBlock
                            required property string modelData
                            Layout.fillWidth: true
                            sourceComponent: desktopBlock.modelData === "forecast" || desktopBlock.modelData === "vitals" ? stripBlock : rowBlock
                            Component {
                                id: rowBlock
                                BlockRow {
                                    glyph: ({ profile: "account_circle", context: "select_window", agenda: "event_upcoming", modules: "widgets" })[desktopBlock.modelData] ?? "circle"
                                    title: ({ profile: SystemInfo.displayName || SystemInfo.username || Translation.tr("You"),
                                        context: pageRoot.lastWindow?.title || Translation.tr("Current app"),
                                        agenda: Translation.tr("Up next"), modules: Translation.tr("Modules") })[desktopBlock.modelData] ?? ""
                                    detail: ({ profile: Translation.tr("Up %1").arg(DateTime.uptime), context: AppSearch.lookupDesktopEntry(pageRoot.lastWindow?.app_id ?? "")?.name || Translation.tr("Workspace"),
                                        agenda: Translation.tr("This week is clear"), modules: Translation.tr("Your desktop widgets") })[desktopBlock.modelData] ?? ""
                                }
                            }
                            Component {
                                id: stripBlock
                                Rectangle {
                                    implicitHeight: stripRow.implicitHeight + (pageRoot.grouped ? Math.round(20 * root.d) : 0)
                                    radius: IrisStyle.radiusTile
                                    color: pageRoot.grouped ? IrisStyle.fillQuiet : "transparent"
                                    RowLayout {
                                        id: stripRow
                                        anchors.centerIn: parent
                                        width: parent.width - (pageRoot.grouped ? Math.round(20 * root.d) : 0)
                                        spacing: 0
                                        Repeater {
                                            model: desktopBlock.modelData === "forecast"
                                                ? (pageRoot.hours.length > 0 ? pageRoot.hours : [{}, {}, {}, {}, {}, {}])
                                                : [{ glyph: "memory", value: Math.round(ResourceUsage.cpuUsage * 100) + "%" },
                                                    { glyph: "memory_alt", value: Math.round(ResourceUsage.memoryUsedPercentage * 100) + "%" },
                                                    { glyph: "device_thermostat", value: ResourceUsage.maxTemp + "°" },
                                                    { glyph: "hard_drive", value: Math.round(ResourceUsage.diskUsedPercentage * 100) + "%" }]
                                            ColumnLayout {
                                                id: stripCell
                                                required property var modelData
                                                required property int index
                                                Layout.fillWidth: true
                                                Layout.preferredWidth: 1
                                                spacing: Math.round(4 * root.d)
                                                IrisText {
                                                    visible: desktopBlock.modelData === "forecast"
                                                    Layout.alignment: Qt.AlignHCenter
                                                    text: stripCell.index === 0 ? Translation.tr("Now") : String(stripCell.modelData.label ?? "").slice(0, 2)
                                                    color: IrisStyle.muted
                                                    font.pixelSize: 11.5 * IrisStyle.typeScale
                                                }
                                                MaterialSymbol {
                                                    Layout.alignment: Qt.AlignHCenter
                                                    text: desktopBlock.modelData === "forecast" ? (Icons.getWeatherIcon(stripCell.modelData.code, stripCell.modelData.isNight) ?? "cloud") : stripCell.modelData.glyph
                                                    fill: 1
                                                    iconSize: Math.round(17 * root.d)
                                                    color: desktopBlock.modelData === "forecast" ? IrisStyle.skyLight(text) : IrisStyle.subtext
                                                }
                                                IrisText {
                                                    Layout.alignment: Qt.AlignHCenter
                                                    text: desktopBlock.modelData === "forecast" ? String(stripCell.modelData.temp ?? "—") : stripCell.modelData.value
                                                    font.family: IrisStyle.fontNumbers
                                                    font.pixelSize: 12.5 * IrisStyle.typeScale
                                                }
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }

                    Repeater {
                        model: pageRoot.mode === "media" ? pageRoot.mediaBlocks : []
                        Loader {
                            id: mediaBlock
                            required property string modelData
                            Layout.fillWidth: true
                            sourceComponent: ({ player: playerBlock, timeline: timelineBlock, transport: transportBlock, players: playersBlock, levels: levelsBlock })[mediaBlock.modelData] ?? null
                            Component {
                                id: playerBlock
                                RowLayout {
                                    spacing: Math.round(14 * root.d)
                                    ClippingRectangle {
                                        implicitWidth: Math.round(56 * root.d); implicitHeight: implicitWidth
                                        radius: IrisStyle.radiusTile
                                        color: IrisStyle.fill
                                        Image { anchors.fill: parent; source: String(pageRoot.player?.trackArtUrl ?? ""); fillMode: Image.PreserveAspectCrop; sourceSize.width: 120 }
                                        MaterialSymbol { anchors.centerIn: parent; visible: String(pageRoot.player?.trackArtUrl ?? "").length === 0; text: "music_note"; fill: 1; iconSize: Math.round(26 * root.d); color: IrisStyle.subtext }
                                    }
                                    ColumnLayout {
                                        Layout.fillWidth: true
                                        spacing: Math.round(2 * root.d)
                                        IrisText { Layout.fillWidth: true; text: pageRoot.player?.trackTitle || Translation.tr("Song title"); font.weight: Font.DemiBold; font.pixelSize: 15 * IrisStyle.typeScale; elide: Text.ElideRight }
                                        IrisText { Layout.fillWidth: true; text: pageRoot.player?.trackArtist || Translation.tr("Artist"); color: IrisStyle.subtext; elide: Text.ElideRight }
                                    }
                                }
                            }
                            Component { id: timelineBlock; Level { value: 0.34; tint: IrisStyle.text } }
                            Component {
                                id: transportBlock
                                Item {
                                    implicitHeight: Math.round(30 * root.d)
                                    Row {
                                        anchors.centerIn: parent
                                        spacing: Math.round(34 * root.d)
                                        Repeater {
                                            model: ["fast_rewind", pageRoot.player?.isPlaying ? "pause" : "play_arrow", "fast_forward"]
                                            MaterialSymbol { required property string modelData; text: modelData; fill: 1; iconSize: Math.round(26 * root.d); color: IrisStyle.text }
                                        }
                                    }
                                }
                            }
                            Component {
                                id: playersBlock
                                BlockRow { glyph: "queue_music"; title: Translation.tr("Other players"); detail: Translation.tr("%1 playing").arg(Math.max(1, (MprisController.players ?? []).length)) }
                            }
                            Component {
                                id: levelsBlock
                                ColumnLayout {
                                    spacing: Math.round(10 * root.d)
                                    Rectangle { Layout.fillWidth: true; implicitHeight: 1; color: IrisStyle.hairline }
                                    Repeater {
                                        model: [0.66, 0.9]
                                        RowLayout {
                                            id: levelRow
                                            required property real modelData
                                            required property int index
                                            spacing: Math.round(12 * root.d)
                                            MaterialSymbol { text: levelRow.index === 0 ? "music_note" : "public"; fill: 1; iconSize: Math.round(17 * root.d); color: IrisStyle.subtext }
                                            IrisText { Layout.preferredWidth: Math.round(110 * root.d); text: levelRow.index === 0 ? Translation.tr("Player") : Translation.tr("Browser"); elide: Text.ElideRight }
                                            Level { value: levelRow.modelData; tint: IrisStyle.text }
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
            }
            Caption {
                glyph: pageRoot.mode === "media" ? "music_note" : pageRoot.mode === "pages" ? "view_carousel" : "space_dashboard"
                text: pageRoot.mode === "pages"
                    ? Translation.tr("%1 px · %2 pages, %3 shortcuts").arg(Math.round(pageRoot.pageW / root.d)).arg(pageRoot.pageCount).arg(pageRoot.actionCount)
                    : pageRoot.mode === "media"
                        ? (pageRoot.mediaBlocks.length > 0 ? Translation.tr("%1 blocks, in your order").arg(pageRoot.mediaBlocks.length) : Translation.tr("No blocks: the page is empty"))
                        : (pageRoot.banner ? Translation.tr("Wallpaper header") : Translation.tr("Plain header")) + " · "
                            + (pageRoot.grouped ? Translation.tr("grouped strips") : Translation.tr("plain rows")) + " · "
                            + Translation.tr("%1 blocks").arg(pageRoot.desktopBlocks.length)
            }
        }
    }

    Component {
        id: bubblesScene
        Item {
            id: bubRoot
            readonly property real naturalWidth: Math.round(760 * root.d)
            readonly property real naturalHeight: Math.round(300 * root.d)
            readonly property bool snap: root.opt("iris.bubbles.snap", true)
            readonly property bool attach: root.opt("iris.bubbles.attach", true)
            readonly property bool cluster: root.opt("iris.bubbles.cluster", true)
            readonly property bool opensIsland: String(root.opt("iris.bubbles.opens", "card")) === "island"
            readonly property bool joined: root.opt("iris.appearance.surfaces.cards.joinOrigin", true)
            readonly property real size: IrisFrame.pieceBand
            readonly property real gap: bubRoot.attach ? 0 : Math.round(Math.max(0, Math.min(64, Number(root.opt("iris.bubbles.edgeGap", 20)))) * root.d)
            readonly property real split: bubRoot.cluster ? 0 : Math.round(6 * root.d)
            readonly property real pad: bubRoot.cluster ? Math.round(4 * root.d) : 0
            readonly property point home: Qt.point(width - IrisFrame.band - bubRoot.gap - bubRoot.pad - bubRoot.size, IrisFrame.band + bubRoot.gap + bubRoot.pad)
            readonly property point slot: Qt.point(bubRoot.home.x - bubRoot.size - bubRoot.split, bubRoot.home.y)
            readonly property point start: Qt.point(Math.round(width * 0.3), Math.round(height * 0.5))
            readonly property point drop: Qt.point(bubRoot.slot.x - Math.round(34 * root.d), bubRoot.slot.y + Math.round(46 * root.d))
            property int step: 0
            readonly property bool grabbed: bubRoot.step === 1
            readonly property bool settled: bubRoot.step >= 2 && bubRoot.step <= 5
            readonly property bool open: bubRoot.step === 4 || bubRoot.step === 5
            readonly property point rest: bubRoot.settled ? (bubRoot.snap ? bubRoot.slot : bubRoot.drop) : bubRoot.start
            property real bx: bubRoot.rest.x
            property real by: bubRoot.rest.y
            Behavior on bx { enabled: !bubRoot.grabbed; NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
            Behavior on by { enabled: !bubRoot.grabbed; NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
            Binding { when: bubRoot.grabbed; bubRoot.bx: bubPointer.x - bubRoot.size / 2 + bubPointer.width / 2 }
            Binding { when: bubRoot.grabbed; bubRoot.by: bubPointer.y - bubRoot.size / 2 + bubPointer.height / 2 }
            readonly property bool together: bubRoot.cluster && bubRoot.settled && bubRoot.snap
            readonly property bool framed: bubRoot.attach && bubRoot.settled && bubRoot.snap
            property real openness: bubRoot.open ? 1 : 0
            Behavior on openness { NumberAnimation { duration: bubRoot.open ? IrisStyle.emergeDuration : IrisStyle.recedeDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: bubRoot.open ? IrisStyle.emergeCurve : IrisStyle.recedeCurve } }
            readonly property real islandW: Math.round(150 * root.d + (340 - 150) * root.d * (bubRoot.opensIsland ? bubRoot.openness : 0))
            readonly property real islandH: Math.round(IrisFrame.islandBand + (130 * root.d - IrisFrame.islandBand) * (bubRoot.opensIsland ? bubRoot.openness : 0))
            readonly property var card: ({
                x: Math.round(Math.max(IrisFrame.band + 8 * root.d, Math.min(bubRoot.width - IrisFrame.band - 230 * root.d, bubRoot.bx + bubRoot.size / 2 - 115 * root.d))),
                y: Math.round(bubRoot.by + bubRoot.size + (bubRoot.joined ? IrisStyle.weld : IrisFrame.bodyAir)),
                width: Math.round(230 * root.d),
                height: Math.max(1, Math.round(120 * root.d * bubRoot.openness))
            })

            Timer {
                running: root.playing
                interval: 900
                repeat: true
                triggeredOnStart: true
                onTriggered: {
                    bubRoot.step = (bubRoot.step + 1) % 7
                    if (bubRoot.step === 1 || bubRoot.step === 3) bubPointer.click()
                }
                onRunningChanged: if (!running) bubRoot.step = 2
            }

            Field.IrisField {
                anchors.fill: parent
                shapes: {
                    const out = []
                    const s = bubRoot.size
                    out.push({ x: (bubRoot.width - bubRoot.islandW) / 2, y: IrisFrame.band, width: bubRoot.islandW, height: bubRoot.islandH,
                        radius: Math.min(bubRoot.islandH / 2, IrisStyle.radius), fuse: IrisStyle.fuseEdge, id: "island", joins: "frame", paints: true })
                    if (bubRoot.together) {
                        const w = bubRoot.home.x + s - bubRoot.bx + 2 * bubRoot.pad
                        out.push({ x: bubRoot.bx - bubRoot.pad, y: bubRoot.home.y - bubRoot.pad, width: w, height: s + 2 * bubRoot.pad,
                            radius: IrisStyle.pieceRadius(s + 2 * bubRoot.pad), fuse: bubRoot.framed ? IrisStyle.fuseEdge : IrisStyle.fuse,
                            id: "plate", joins: bubRoot.framed ? "frame" : "", paints: true })
                    } else {
                        out.push({ x: bubRoot.home.x, y: bubRoot.home.y, width: s, height: s, radius: IrisStyle.pieceRadius(s),
                            fuse: bubRoot.attach ? IrisStyle.fuseEdge : IrisStyle.fuse, id: "resident", joins: bubRoot.attach ? "frame" : "", paints: true })
                        out.push({ x: bubRoot.bx, y: bubRoot.by, width: s, height: s, radius: IrisStyle.pieceRadius(s),
                            fuse: bubRoot.framed ? IrisStyle.fuseEdge : IrisStyle.fuse, id: "carried", joins: bubRoot.framed ? "frame" : "", paints: true })
                    }
                    if (!bubRoot.opensIsland && bubRoot.openness > 0.01)
                        out.push(Object.assign({ radius: Math.min(IrisStyle.radiusSheet, bubRoot.card.height / 2), fuse: IrisStyle.fuseDeep,
                            id: "card", joins: bubRoot.joined ? (bubRoot.together ? "plate" : "carried") : "", paints: true }, bubRoot.card))
                    return out
                }
            }

            IrisClock {
                x: (bubRoot.width - width) / 2
                y: IrisFrame.band + (IrisFrame.islandBand - height) / 2
                opacity: 1 - (bubRoot.opensIsland ? bubRoot.openness : 0)
                pixelSize: 15 * IrisStyle.typeScale
                separatorColor: IrisStyle.secondaryAccent
            }
            MaterialSymbol {
                x: bubRoot.home.x + (bubRoot.size - width) / 2
                y: bubRoot.home.y + (bubRoot.size - height) / 2
                text: "partly_cloudy_day"; fill: 1
                iconSize: Math.round(bubRoot.size * 0.46)
                color: IrisStyle.identity.sky
            }
            MaterialSymbol {
                x: bubRoot.bx + (bubRoot.size - width) / 2
                y: bubRoot.by + (bubRoot.size - height) / 2
                scale: bubRoot.grabbed ? 1.08 : 1
                Behavior on scale { NumberAnimation { duration: IrisStyle.duration(140); easing.type: IrisStyle.feedbackEasing } }
                text: "volume_up"; fill: 1
                iconSize: Math.round(bubRoot.size * 0.46)
                color: IrisStyle.identity.indigo
            }

            ColumnLayout {
                visible: bubRoot.openness > 0.02
                opacity: IrisStyle.ramp(bubRoot.openness, IrisStyle.dropRise, IrisStyle.dropSpan)
                x: bubRoot.opensIsland ? (bubRoot.width - bubRoot.islandW) / 2 + Math.round(18 * root.d) : bubRoot.card.x + Math.round(16 * root.d)
                y: bubRoot.opensIsland ? IrisFrame.band + Math.round(16 * root.d) : bubRoot.card.y + Math.round(14 * root.d)
                width: (bubRoot.opensIsland ? bubRoot.islandW : bubRoot.card.width) - Math.round(32 * root.d)
                spacing: Math.round(10 * root.d)
                RowLayout {
                    spacing: Math.round(8 * root.d)
                    MaterialSymbol { text: "volume_up"; fill: 1; iconSize: Math.round(18 * root.d); color: IrisStyle.identity.indigo }
                    IrisText { text: Translation.tr("Sound"); font.weight: Font.DemiBold }
                }
                Level { value: 0.62; tint: IrisStyle.identity.indigo }
                IrisText { text: Translation.tr("Speakers"); color: IrisStyle.muted; font.pixelSize: 11.5 * IrisStyle.typeScale }
            }

            Pointer {
                id: bubPointer
                grabbing: bubRoot.grabbed
                travel: bubRoot.step === 1 ? 820 : 560
                x: bubRoot.step === 0 ? bubRoot.start.x + bubRoot.size * 0.55
                    : bubRoot.step === 1 ? bubRoot.drop.x + bubRoot.size / 2 - width / 2
                    : bubRoot.step === 2 ? bubRoot.drop.x + bubRoot.size * 0.9
                    : bubRoot.step === 6 ? bubRoot.width * 0.45
                    : bubRoot.rest.x + bubRoot.size * 0.55
                y: bubRoot.step === 0 ? bubRoot.start.y + bubRoot.size * 0.55
                    : bubRoot.step === 1 ? bubRoot.drop.y + bubRoot.size / 2 - height / 2
                    : bubRoot.step === 2 ? bubRoot.drop.y + bubRoot.size * 1.4
                    : bubRoot.step === 6 ? bubRoot.height * 0.72
                    : bubRoot.rest.y + bubRoot.size * 0.55
            }

            Caption {
                glyph: ["near_me", "back_hand", bubRoot.snap ? "select" : "pan_tool", "touch_app", bubRoot.opensIsland ? "pill" : "web_asset", bubRoot.opensIsland ? "pill" : "web_asset", "undo"][bubRoot.step]
                text: [Translation.tr("A bubble floats over the desktop"),
                    Translation.tr("Drag it towards a corner"),
                    !bubRoot.snap ? Translation.tr("Snap off: it stays where you let go")
                        : (bubRoot.attach ? Translation.tr("Snaps onto the frame") : Translation.tr("Snaps %1 px from the edges").arg(Math.round(bubRoot.gap / root.d)))
                            + (bubRoot.cluster ? " · " + Translation.tr("grouped into a bar") : ""),
                    Translation.tr("Tap it"),
                    bubRoot.opensIsland ? Translation.tr("Opens the Island") : bubRoot.joined ? Translation.tr("Grows its card, joined to it") : Translation.tr("Grows its card"),
                    bubRoot.opensIsland ? Translation.tr("Opens the Island") : bubRoot.joined ? Translation.tr("Grows its card, joined to it") : Translation.tr("Grows its card"),
                    Translation.tr("And back")][bubRoot.step]
            }
        }
    }

    Component {
        id: reserveScene
        Item {
            id: reserveRoot
            readonly property real naturalWidth: Math.round(640 * root.d)
            readonly property real naturalHeight: Math.round(280 * root.d)
            readonly property bool reserve: root.opt("iris.bar.reserveSpace", true)
            readonly property string edge: IrisFrame.islandEdge
            readonly property bool bottomEdge: reserveRoot.edge === "bottom"
            readonly property bool vertical: reserveRoot.edge === "left" || reserveRoot.edge === "right"
            readonly property real islandSpan: IrisFrame.band + IrisFrame.islandMargin + IrisFrame.islandBand
            function inset(side: string): real {
                if (side === reserveRoot.edge) return reserveRoot.reserve ? reserveRoot.islandSpan + Math.round(8 * root.d) : IrisFrame.band
                return Math.round((side === "top" || side === "bottom" ? 24 : 40) * root.d)
            }
            Rectangle {
                x: reserveRoot.inset("left")
                width: parent.width - x - reserveRoot.inset("right")
                y: reserveRoot.inset("top")
                height: parent.height - y - reserveRoot.inset("bottom")
                Behavior on x { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Behavior on width { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                radius: IrisStyle.radiusTile
                color: IrisStyle.surfaceHigh
                border.width: 1
                border.color: IrisStyle.border
                Behavior on y { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Behavior on height { NumberAnimation { duration: IrisStyle.moveDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.moveCurve } }
                Row {
                    x: Math.round(12 * root.d); y: Math.round(11 * root.d)
                    spacing: Math.round(6 * root.d)
                    Repeater { model: 3; Rectangle { required property int index; width: Math.round(10 * root.d); height: width; radius: width / 2; color: IrisStyle.fillStrong } }
                }
                Rectangle { x: Math.round(12 * root.d); y: Math.round(38 * root.d); width: parent.width * 0.42; height: Math.round(9 * root.d); radius: height / 2; color: IrisStyle.fill }
                Rectangle { x: Math.round(12 * root.d); y: Math.round(56 * root.d); width: parent.width * 0.6; height: Math.round(9 * root.d); radius: height / 2; color: IrisStyle.fillQuiet }
            }
            IslandPill {
                readonly property real inset: IrisFrame.band + IrisFrame.islandMargin
                vertical: reserveRoot.vertical
                x: reserveRoot.edge === "left" ? inset : reserveRoot.edge === "right" ? parent.width - inset - width : Math.round((parent.width - width) / 2)
                y: reserveRoot.edge === "top" ? inset : reserveRoot.bottomEdge ? parent.height - inset - height : Math.round((parent.height - height) / 2)
            }
            Caption {
                anchors.bottom: reserveRoot.bottomEdge ? undefined : parent.bottom
                anchors.top: reserveRoot.bottomEdge ? parent.top : undefined
                glyph: !reserveRoot.reserve ? "layers" : reserveRoot.edge === "left" ? "align_horizontal_left"
                    : reserveRoot.edge === "right" ? "align_horizontal_right" : reserveRoot.bottomEdge ? "vertical_align_bottom" : "vertical_align_top"
                text: !reserveRoot.reserve ? Translation.tr("Windows run under the Island")
                    : reserveRoot.vertical ? Translation.tr("Windows start beside the Island")
                    : reserveRoot.bottomEdge ? Translation.tr("Windows end above the Island") : Translation.tr("Windows start below the Island")
            }
        }
    }

    Component {
        id: interactionScene
        Item {
            id: actRoot
            readonly property real naturalWidth: Math.round(620 * root.d)
            readonly property real naturalHeight: Math.round(280 * root.d)
            readonly property bool hover: root.opt("iris.bar.hoverExpand", true)
            readonly property int delay: Math.max(120, Math.min(800, Number(root.opt("iris.bar.hoverDelay", 300))))
            readonly property string scroll: String(root.opt("iris.bar.scrollAction", "volume"))
            readonly property bool events: root.opt("iris.bar.events", true)
            readonly property bool caps: actRoot.events && root.opt("keyboardIndicators.popup.caps", true)
            property int step: 0
            property bool expanded: false
            property bool clicked: false
            property real level: 0.35
            readonly property real restW: Math.round(150 * root.d)
            readonly property real restH: IrisFrame.islandBand
            readonly property real pillW: actRoot.expanded ? Math.round(360 * root.d) : actRoot.step === 3 && actRoot.events ? Math.round(260 * root.d) : actRoot.restW
            readonly property real pillH: actRoot.expanded ? Math.round(150 * root.d) : actRoot.restH
            Timer {
                running: root.playing
                interval: 1700
                repeat: true
                triggeredOnStart: true
                onTriggered: {
                    actRoot.step = (actRoot.step + 1) % 4
                    actRoot.expanded = false
                    actRoot.clicked = false
                    if (actRoot.step === 1) expandTimer.restart()
                    if (actRoot.step === 2) actRoot.level = 0.35
                }
            }
            Timer {
                id: expandTimer
                interval: actRoot.hover ? actRoot.delay : 420
                onTriggered: { actRoot.clicked = !actRoot.hover; actRoot.expanded = true }
            }
            Timer {
                running: root.playing && actRoot.step === 2 && actRoot.scroll !== "none"
                interval: 120
                repeat: true
                onTriggered: actRoot.level = Math.min(0.85, actRoot.level + 0.05)
            }
            Rectangle {
                id: pill
                anchors.horizontalCenter: parent.horizontalCenter
                y: IrisFrame.band + IrisFrame.islandMargin
                width: actRoot.pillW
                height: actRoot.pillH
                radius: actRoot.expanded ? IrisStyle.radius : height / 2
                color: IrisStyle.bodySurface
                border.width: IrisStyle.rim.a > 0 ? 1 : 0
                border.color: IrisStyle.rim
                Behavior on width { NumberAnimation { duration: IrisStyle.morphDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.morphCurve } }
                Behavior on height { NumberAnimation { duration: IrisStyle.morphDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.morphCurve } }
                Behavior on radius { NumberAnimation { duration: IrisStyle.morphDuration; easing.type: Easing.BezierSpline; easing.bezierCurve: IrisStyle.morphCurve } }
                IrisClock {
                    anchors.centerIn: parent
                    visible: !actRoot.expanded && !(actRoot.step === 2 && actRoot.scroll !== "none") && !(actRoot.step === 3 && actRoot.events)
                    pixelSize: 15 * IrisStyle.typeScale
                    separatorColor: IrisStyle.secondaryAccent
                }
                RowLayout {
                    anchors.fill: parent
                    anchors.leftMargin: Math.round(14 * root.d)
                    anchors.rightMargin: Math.round(16 * root.d)
                    visible: actRoot.step === 2 && actRoot.scroll !== "none" && !actRoot.expanded
                    spacing: Math.round(8 * root.d)
                    MaterialSymbol { text: actRoot.scroll === "brightness" ? "light_mode" : "volume_up"; fill: 1; iconSize: Math.round(16 * root.d); color: IrisStyle.text }
                    Level { value: actRoot.level; tint: IrisStyle.text }
                }
                RowLayout {
                    anchors.centerIn: parent
                    visible: actRoot.step === 3 && actRoot.events && !actRoot.expanded
                    spacing: Math.round(8 * root.d)
                    MaterialSymbol { text: "battery_charging_full"; fill: 1; iconSize: Math.round(17 * root.d); color: IrisStyle.identity.green }
                    IrisText { text: Translation.tr("Charger connected"); font.weight: Font.DemiBold }
                }
                ColumnLayout {
                    anchors.fill: parent
                    anchors.margins: Math.round(18 * root.d)
                    visible: actRoot.expanded
                    opacity: actRoot.expanded ? 1 : 0
                    Behavior on opacity { NumberAnimation { duration: IrisStyle.duration(200) } }
                    IrisText { text: Qt.locale().toString(DateTime.clock.date, "dddd d MMMM"); color: IrisStyle.secondaryAccent; font.weight: Font.DemiBold }
                    IrisText { text: Qt.locale().toString(DateTime.clock.date, "hh:mm"); font.family: IrisStyle.fontNumbers; font.weight: IrisStyle.figureWeight; font.pixelSize: 40 * IrisStyle.typeScale }
                    Item { Layout.fillHeight: true }
                    Level { value: 0.6 }
                }
            }
            Rectangle {
                anchors.horizontalCenter: pill.horizontalCenter
                anchors.top: pill.bottom
                anchors.topMargin: Math.round(8 * root.d)
                visible: actRoot.step === 3 && actRoot.caps
                width: capsLabel.implicitWidth + Math.round(24 * root.d)
                height: Math.round(26 * root.d)
                radius: height / 2
                color: IrisStyle.bodySurface
                IrisText { id: capsLabel; anchors.centerIn: parent; text: Translation.tr("Caps Lock on"); font.pixelSize: 11.5 * IrisStyle.typeScale; font.weight: Font.DemiBold }
            }
            MaterialSymbol {
                id: pointer
                text: actRoot.step === 2 ? "mouse" : "arrow_selector_tool"
                fill: 1
                iconSize: Math.round(22 * root.d)
                color: "white"
                x: actRoot.step === 0 ? parent.width * 0.72 : pill.x + pill.width * 0.55
                y: actRoot.step === 0 ? parent.height * 0.7 : pill.y + Math.min(pill.height - 8, IrisFrame.islandBand * 0.6)
                visible: actRoot.step < 3
                Behavior on x { NumberAnimation { duration: 520; easing.type: Easing.OutCubic } }
                Behavior on y { NumberAnimation { duration: 520; easing.type: Easing.OutCubic } }
                Rectangle {
                    anchors.centerIn: parent
                    width: actRoot.clicked ? parent.width * 1.6 : 0
                    height: width
                    radius: width / 2
                    color: "transparent"
                    border.width: 2
                    border.color: IrisStyle.accent
                    opacity: actRoot.clicked ? 0 : 1
                    Behavior on width { NumberAnimation { duration: 320 } }
                    Behavior on opacity { NumberAnimation { duration: 420 } }
                }
            }
            Caption {
                glyph: ["near_me", actRoot.hover ? "timer" : "ads_click", "swipe_vertical", "campaign"][actRoot.step]
                text: [Translation.tr("Resting"),
                    actRoot.hover ? Translation.tr("Opens after resting %1 ms").arg(actRoot.delay) : Translation.tr("Opens on a click"),
                    actRoot.scroll === "none" ? Translation.tr("Scroll does nothing") : actRoot.scroll === "brightness" ? Translation.tr("Scroll sets brightness") : Translation.tr("Scroll sets volume"),
                    actRoot.events ? (actRoot.caps ? Translation.tr("System events and the Caps Lock badge") : Translation.tr("System events")) : Translation.tr("System events off")][actRoot.step]
            }
        }
    }
}
